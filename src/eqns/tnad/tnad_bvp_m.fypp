! Module  : tnad_bvp_m
! Purpose : nonadiabatic (+turbulent convection) boundary value problem solver
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module tnad_bvp_m

   ! Uses

   use forum_m, only: RD

   use bound_m
   use bvp_m
   use context_m
   use diff_element_m
   use ext_m
   use grid_m
   use model_m
   use mode_par_m
   use num_par_m
   use osc_par_m
   use point_m
   use state_m
   use sysmtx_m
   use trans_m
   use turb_m
   use wave_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(bvp_ct) :: tnad_bvp_t
      private
      type(context_t), pointer     :: cx => null()
      class(trans_ct), allocatable :: tr(:)
      real(RD), allocatable        :: V_g(:)
      real(RD), allocatable        :: c_1(:)
      real(RD), allocatable        :: ups_T(:)
      real(RD), allocatable        :: L_conv(:)
      real(RD), allocatable        :: tau_conv(:)
      real(RD), allocatable        :: Omega_rot(:)
      complex(RD)                  :: lambda
      complex(RD)                  :: l_i
      type(grid_t)                 :: gr
      type(mode_par_t)             :: md_p
      type(num_par_t)              :: nm_p
      type(osc_par_t)              :: os_p
   end type tnad_bvp_t

   ! Interfaces

   interface tnad_bvp_t
      module procedure tnad_bvp_t_
   end interface tnad_bvp_t

   interface wave_t
      module procedure wave_t_hom_
      module procedure wave_t_inhom_
   end interface wave_t

   ! Access specifiers

   public :: tnad_bvp_t
   public :: wave_t

   ! Default access

   private

contains

   function tnad_bvp_t_(cx, gr, md_p, nm_p, os_p) result(bp)

      use gyre_nad_bound_m

      use gyre_nad_trans_m

      use tnad_diff_m

      class(context_t), pointer, intent(in) :: cx
      type(grid_t), intent(in)              :: gr
      type(mode_par_t), intent(in)          :: md_p
      type(num_par_t), intent(in)           :: nm_p
      type(osc_par_t), intent(in)           :: os_p
      type(tnad_bvp_t)                      :: bp

      class(bound_ct), allocatable       :: bd
      integer                            :: j
      type(diff_element_ct), allocatable :: de(:)
      class(model_t), pointer            :: ml
      real(RD)                           :: V
      real(RD)                           :: As

      ! Construct the tnad_bvp_t

      ! Initialize the boundary conditions

      select case(os_p%variables_set)
      case('GYRE')
         bd = gyre_nad_bound_t(cx, os_p)
      case default
         @:ABORT('invalid variables_set')
      end select

      ! Initialize the difference equations

      allocate(de(gr%n-1))

      !$OMP PARALLEL DO
      do j = 1, gr%n-1
         allocate(de(j)%df, SOURCE=tnad_diff_t(cx, gr%pt(j), gr%pt(j+1), nm_p, os_p))
      end do

      ! Initialize the bvp_t

      bp%bvp_ct = bvp_ct(bd, de, nm_p)

      ! Other initializations

      ml => cx%model()

      allocate(bp%V_g(gr%n))
      allocate(bp%c_1(gr%n))
      allocate(bp%ups_T(gr%n))
      allocate(bp%L_conv(gr%n))
      allocate(bp%tau_conv(gr%n))
      allocate(bp%Omega_rot(gr%n))

      !$OMP PARALLEL DO PRIVATE(V, As)
      do j = 1, gr%n

         associate(pt => gr%pt(j))

            V = ml%coeff(I_V_2, pt)*pt%x**2

            bp%V_g(j) = V/ml%coeff(I_C_1, pt)
            bp%c_1(j) = ml%coeff(I_C_1, pt)
            bp%ups_T(j) = ml%coeff(I_UPS_T, pt)

            if (pt%x /= 0._RD) then

               As = ml%coeff(I_AS, pt)

               bp%L_conv(j) = MIN(1._RD/V, 1._RD)*cx%alpha_trb
               bp%tau_conv(j) = tau_conv(As, bp%c_1(j))

            end if

            bp%Omega_rot(j) = ml%coeff(I_OMEGA_ROT, pt)

         end associate

      end do

      if (cx%angular_cache) then
         bp%lambda = cx%lambda(cx%Omega_rot_i, state_ct(0._RD))
         bp%l_i = cx%l_e(cx%Omega_rot_i, state_ct(0._RD))
      end if

      bp%cx => cx
      bp%gr = gr

      select case(os_p%variables_set)
      case('GYRE')
         bp%tr = gyre_nad_trans_t(cx, gr%pt)
      case default
         @:ABORT('invalid variables_set')
      end select

      bp%md_p = md_p
      bp%nm_p = nm_p
      bp%os_p = os_p

      ! Finish

      return

   end function tnad_bvp_t_

   !****

   function wave_t_hom_(bp, sm, st, id, chi, n_iter) result(wv)

      class(tnad_bvp_t), intent(in)   :: bp
      class(sysmtx_ct), intent(inout) :: sm
      type(state_ct), intent(in)      :: st
      integer, intent(in)             :: id
      real(RD), intent(in)            :: chi
      integer, intent(in)             :: n_iter
      type(wave_t)                    :: wv

      complex(RD) :: y(6,bp%n)
      integer     :: j

      ! Calculate the homogeneous solution vector

      call bp%solve_hom(sm, st, y)

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr(j)%transform(st, y(:,j))
      end do

      ! Construct the wave_t

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_hom_

   !****

   function wave_t_inhom_(bp, sm, st, z_i, z_o, id, chi, n_iter) result(wv)

      class(tnad_bvp_t), intent(in)   :: bp
      class(sysmtx_ct), intent(inout) :: sm
      type(state_ct), intent(in)      :: st
      complex(RD), intent(in)         :: z_i(:)
      complex(RD), intent(in)         :: z_o(:)
      integer, intent(in)             :: id
      real(RD), intent(in)            :: chi
      integer, intent(in)             :: n_iter
      type(wave_t)                    :: wv

      complex(RD) :: y(6,bp%n)
      integer     :: j

      @:CHECK_BOUNDS(SIZE(z_i), bp%n_i)
      @:CHECK_BOUNDS(SIZE(z_o), bp%n_o)

      ! Calculate the inhomogeneous solution vector

      call bp%solve_inhom(sm, st, z_i, z_o, y)

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr(j)%transform(st, y(:,j))
      end do

      ! Construct the wave_t

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_inhom_

   !****

   function wave_t_y_(bp, st, y, id, chi, n_iter) result(wv)

      class(tnad_bvp_t), intent(in) :: bp
      type(state_ct), intent(in)    :: st
      complex(RD), intent(in)       :: y(:,:)
      integer, intent(in)           :: id
      real(RD), intent(in)          :: chi
      integer, intent(in)           :: n_iter
      type(wave_t)                  :: wv

      integer     :: j
      complex(RD) :: omega_c
      real(RD)    :: omega_c_r
      complex(RD) :: i_omega_c
      complex(RD) :: lambda
      complex(RD) :: l_i
      complex(RD) :: om2
      complex(RD) :: eta
      complex(RD) :: F_trb
      complex(RD) :: Q(1,6)
      complex(RD) :: y_trb
      complex(RD) :: y_c(6,bp%n)

      @:CHECK_BOUNDS(SIZE(y, 1), bp%n_e)
      @:CHECK_BOUNDS(SIZE(y, 2), bp%n)

      ! Transform the variables to canonical form, by subtracting off
      ! the y_trb contribution

      !$OMP PARALLEL DO
      do j = 1, bp%n

         associate( &
            omega => st%omega, &
            omega_r => st%omega_r, &
            V_g => bp%V_g(j), &
            c_1 => bp%c_1(j), &
            ups_T => bp%ups_T(j), &
            L_conv => bp%L_conv(j), &
            tau_conv => bp%tau_conv(j), &
            Omega_rot => bp%Omega_rot(j), &
            Omega_rot_i => bp%cx%Omega_rot_i, &
            alpha_grv => bp%cx%alpha_grv, &
            alpha_omg => bp%cx%alpha_omg, &
            alpha_gam => bp%cx%alpha_gam, &
            m => bp%cx%m, &
            angular_cache => bp%cx%angular_cache)

            omega_c = omega - m*Omega_rot
            omega_c_r = omega_r - m*Omega_rot

            i_omega_c = (0._RD,1._RD)*sqrt(CMPLX(alpha_omg, KIND=RD))*omega_c

            if (angular_cache) then
               lambda = bp%lambda
               l_i = bp%l_i
            else
               lambda = bp%cx%lambda(Omega_rot, st)
               l_i = bp%cx%l_e(Omega_rot_i, st)
            end if

            om2 = c_1*alpha_omg*omega_c**2
            eta = lambda/om2

            F_trb = i_omega_c*c_1*nu_trb(omega_c_r, L_conv, tau_conv)

            ! Set upt the Q vector

            #:include 'Q.inc'

            ! Evaluate y_trb

            y_trb = DOT_PRODUCT(Q(1,:), y(:,j))

         end associate

         y_c(:,j) = y(:,j)
         y_c(2,j) = y_c(2,j) - y_trb

      end do

      ! Construct the wave_t

      wv = wave_t(st, y_c, bp%cx, bp%gr, bp%md_p, bp%nm_p, bp%os_p, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_y_

end module tnad_bvp_m
