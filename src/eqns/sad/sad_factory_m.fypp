! Module  : sad_factory_m
! Purpose : factory methods for static adiabatic routines
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module sad_factory_m

   ! Uses

   use forum_m, only: RD

   use bound_m
   use context_m
   use eqns_m
   use osc_par_m
   use point_m
   use trans_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Interfaces

   interface sad_eqns_t
      module procedure sad_eqns_t_0_
      module procedure sad_eqns_t_1_
   end interface sad_eqns_t

   interface sad_trans_t
      module procedure sad_trans_t_0_
      module procedure sad_trans_t_1_
   end interface sad_trans_t

   ! Access specifiers

   public :: sad_eqns_t
   public :: sad_eqns_t_0_
   public :: sad_eqns_t_1_
   public :: sad_trans_t
   public :: sad_trans_t_0_
   public :: sad_trans_t_1_
   public :: sad_ibound_t
   public :: sad_obound_t

   ! Default access

   private

contains

   #:for R in (0, 1)

      function sad_eqns_t_${R}$_(cx, pt, os_p) result(eq)

         use gyre_sad_eqns_m

         type(context_t), pointer, intent(in) :: cx
         type(point_t), intent(in)            :: pt${ARRAY_SPEC(R)}$
         type(osc_par_t), intent(in)          :: os_p
         class(eqns_rt), allocatable          :: eq${ARRAY_SPEC(R)}$

         ! Construct the sad_eqns type

         select case(os_p%variables_set)
         case('GYRE')
            eq = gyre_sad_eqns_t(cx, pt)
         case default
            @:ABORT('invalid variables_set')
         end select

         ! Finish

         return

      end function sad_eqns_t_${R}$_

      !****

      function sad_trans_t_${R}$_(cx, pt, os_p) result(tr)

         use gyre_sad_trans_m

         type(context_t), pointer, intent(in) :: cx
         type(point_t), intent(in)            :: pt${ARRAY_SPEC(R)}$
         type(osc_par_t), intent(in)          :: os_p
         class(trans_rt), allocatable         :: tr${ARRAY_SPEC(R)}$

         ! Construct the sad_trans type

         select case(os_p%variables_set)
         case('GYRE')
            tr = gyre_sad_trans_t(cx, pt)
         case default
            @:ABORT('invalid variables_set')
         end select

         ! Finish

         return

      end function sad_trans_t_${R}$_

   #:endfor

   !****

   function sad_ibound_t(cx, os_p) result(bd)

      use gyre_sad_ibound_m

      type(context_t), pointer, intent(in) :: cx
      type(osc_par_t), intent(in)          :: os_p
      class(bound_rt), allocatable         :: bd

      ! Construct the sad_ibound type

      select case(os_p%variables_set)
      case('GYRE')
         bd = gyre_sad_ibound_t(cx, os_p)
      case default
         @:ABORT('invalid variables_set')
      end select

      ! Finish

      return

   end function sad_ibound_t

   !****

   function sad_obound_t(cx, os_p) result(bd)

      use gyre_sad_obound_m

      type(context_t), pointer, intent(in) :: cx
      type(osc_par_t), intent(in)          :: os_p
      class(bound_rt), allocatable         :: bd

      ! Construct the sad_obound type

      select case(os_p%variables_set)
      case('GYRE')
         bd = gyre_sad_obound_t(cx, os_p)
      case default
         @:ABORT('invalid variables_set')
      end select

      ! Finish

      return

   end function sad_obound_t

end module sad_factory_m
