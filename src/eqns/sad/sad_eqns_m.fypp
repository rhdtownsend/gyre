! Module  : sad_eqns_m
! Purpose : static adiabatic differential equations
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

#:set P = os.path.basename(_FILE_).split('_')[0]

module ${P}$_sad_eqns_m

   ! Uses

   use forum_m, only: RD

   use context_m
   use eqns_m
   use model_m
   use model_util_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(eqns_rt) :: ${P}$_sad_eqns_t
      private
      type(context_t), pointer :: cx => null()
      real(RD)                 :: V_g
      real(RD)                 :: As
      real(RD)                 :: U
      real(RD)                 :: x
   contains
      private
      procedure, public :: eval_A
      procedure, public :: eval_C
   end type ${P}$_sad_eqns_t

   ! Interfaces

   interface ${P}$_sad_eqns_t
      module procedure ${P}$_sad_eqns_t_0_
      module procedure ${P}$_sad_eqns_t_1_
   end interface ${P}$_sad_eqns_t

   ! Access specifiers

   public :: ${P}$_sad_eqns_t

   ! Default access

   private

contains

   function ${P}$_sad_eqns_t_0_(cx, pt) result(eq)

      class(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)             :: pt
      type(${P}$_sad_eqns_t)                :: eq

      class(model_t), pointer :: ml

      ! Construct the ${P}$_sad_eqns_t

      eq%cx => cx

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [I_V_2,I_AS,I_U,I_GAMMA_1])

      @:ASSERT(.NOT. ml%is_vacuum(pt), 'vacuum point')

      eq%V_g = ml%coeff(I_V_2, pt)*pt%x**2/ml%coeff(I_GAMMA_1, pt)
      eq%As = ml%coeff(I_AS, pt)
      eq%U = ml%coeff(I_U, pt)

      ! Other initializations

      eq%x = pt%x

      eq%n_e = 2

      ! Finish

      return

   end function ${P}$_sad_eqns_t_0_

   !****

   function ${P}$_sad_eqns_t_1_(cx, pt) result(eq)

      type(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)            :: pt(:)
      type(${P}$_sad_eqns_t)               :: eq(SIZE(pt))

      integer :: j

      ! Construct the array of ${P}$_sad_eqns types

      do j = 1, SIZE(pt)
         eq(j) = ${P}$_sad_eqns_t(cx, pt(j))
      end do

      ! Finish

      return

   end function ${P}$_sad_eqns_t_1_

   !****

   subroutine eval_A(this, st, A)

      class(${P}$_sad_eqns_t), intent(in) :: this
      class(state_rt), intent(in)         :: st
      real(RD), intent(out)               :: A(:,:)

      real(RD) :: l
      real(RD) :: lambda
      real(RD) :: invx

      @:CHECK_BOUNDS(SIZE(A, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(A, 2), this%n_e)

      ! Evaluate the Jacobian matrix

      associate ( &
         V_g => this%V_g, &
         As => this%As, &
         U => this%U, &
         x => this%x, &
         l_e => this%cx%l)

         lambda = l_e*(l_e+1._RD)

         invx = 1._RD/x

         ! Set up the matrix

         #:include 'A.inc'

      end associate

      ! Finish

      return

   end subroutine eval_A

   !****

   subroutine eval_C(this, st, C)

      class(${P}$_sad_eqns_t), intent(in) :: this
      class(state_rt), intent(in)   :: st
      real(RD), intent(out)         :: C(:,:)

      @:CHECK_BOUNDS(SIZE(C, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(C, 2), this%n_e)

      ! Evaluate the match matrix

      associate ( &
         U => this%U)

         ! Set up the matrix

         #:include 'C.inc'

      end associate

      ! Finish

      return

   end subroutine eval_C

end module ${P}$_sad_eqns_m
