! Module  : sad_bound_m
! Purpose : static adiabatic boundary conditions
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

#:set P = os.path.basename(_FILE_).split('_')[0]

module ${P}$_sad_bound_m

   ! Uses

   use forum_m, only: RD

   use bound_m
   use context_m
   use model_m
   use model_util_m
   use osc_par_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(bound_rt) :: ${P}$_sad_bound_t
      private
      type(context_t), pointer   :: cx => null()
      real(RD)                   :: U_o
      procedure(build_), pointer :: build_i_
      procedure(build_), pointer :: build_o_
   contains
      private
      procedure, public :: build_i
      procedure, public :: build_o
   end type ${P}$_sad_bound_t

   ! Interfaces

   interface
      subroutine build_(this, st, B)
         use forum_m, only: RD
         use state_m
         import ${P}$_sad_bound_t
         class(${P}$_sad_bound_t), intent(in) :: this
         class(state_rt), intent(in)          :: st
         real(RD), intent(out)                :: B(:,:)
      end subroutine build_
   end interface

   interface ${P}$_sad_bound_t
      module procedure ${P}$_sad_bound_t_
   end interface ${P}$_sad_bound_t

   ! Access specifiers

   public :: ${P}$_sad_bound_t

   ! Default access

   private

contains

   function ${P}$_sad_bound_t_(cx, os_p) result(bd)

      type(context_t), pointer, intent(in) :: cx
      type(osc_par_t), intent(in)          :: os_p
      type(${P}$_sad_bound_t)              :: bd

      class(model_t), pointer :: ml
      type(point_t)           :: pt_i
      type(point_t)           :: pt_o

      ! Construct the ${P}$_sad_bound_t

      ml => cx%model()
      pt_i = cx%point_i()
      pt_o = cx%point_o()

      select case (os_p%inner_bound)
      case ('REGULAR')
         @:ASSERT(pt_i%x == 0._RD, 'boundary condition invalid for x /= 0')
         bd%build_i_ => build_i_regular_
      case default
         @:ABORT('invalid inner_bound')
      end select

      select case (os_p%outer_bound)
      case ('VACUUM')
         bd%build_o_ => build_o_vacuum_
      case default
         @:ABORT('invalid outer_bound')
      end select

      bd%cx => cx

      ! Store structure coefficients

      bd%U_o = ml%coeff(I_U, pt_o)

      ! Other initializations

      bd%n_i = 1
      bd%n_o = 1

      bd%n_e = 2

      ! Finish

      return

   end function ${P}$_sad_bound_t_

   !****

   subroutine build_i(this, st, B)

      class(${P}$_sad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      ! Evaluate the inner boundary conditions

      call this%build_i_(st, B)

      ! Finish

      return

   end subroutine build_i

   !****

   subroutine build_i_regular_(this, st, B)

      class(${P}$_sad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_i)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the inner boundary conditions (regular-enforcing)

      associate (&
         l_e => this%cx%l)

         ! Set up the matrix

         #:include 'B_i_regular.inc'

      end associate

      ! Finish

      return

   end subroutine build_i_regular_

   !****

   subroutine build_o(this, st, B)

      class(${P}$_sad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      ! Evaluate the outer boundary conditions

      call this%build_o_(st, B)

      ! Finish

      return

   end subroutine build_o

   !****

   subroutine build_o_vacuum_(this, st, B)

      class(${P}$_sad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions (vacuum)

      associate ( &
         U => this%U_o, &
         l_e => this%cx%l)

         ! Set up the matrix

         #:include 'B_o_vacuum.inc'

      end associate

      ! Finish

      return

   end subroutine build_o_vacuum_

end module ${P}$_sad_bound_m
