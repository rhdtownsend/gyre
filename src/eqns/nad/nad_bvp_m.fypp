! Module  : nad_bvp_m
! Purpose : nonadiabatic boundary value problem solver
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module nad_bvp_m

   ! Uses

   use forum_m, only: RD

   use bound_m
   use bvp_m
   use context_m
   use diff_element_m
   use ext_m
   use grid_m
   use model_m
   use mode_par_m
   use num_par_m
   use osc_par_m
   use point_m
   use state_m
   use sysmtx_m
   use trans_m
   use wave_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(bvp_ct) :: nad_bvp_t
      private
      type(context_t), pointer     :: cx => null()
      class(trans_ct), allocatable :: tr(:)
      type(grid_t)                 :: gr
      type(mode_par_t)             :: md_p
      type(num_par_t)              :: nm_p
      type(osc_par_t)              :: os_p
   end type nad_bvp_t

   ! Interfaces

   interface nad_bvp_t
      module procedure nad_bvp_t_
   end interface nad_bvp_t

   interface wave_t
      module procedure wave_t_hom_
      module procedure wave_t_inhom_
   end interface wave_t

   ! Access specifiers

   public :: nad_bvp_t
   public :: wave_t

   ! Default access

   private

contains

   function nad_bvp_t_(cx, gr, md_p, nm_p, os_p) result(bp)

      use gyre_nad_bound_m
      use dziem_nad_bound_m
      use jcd_nad_bound_m
      use rjcd_nad_bound_m
      use mix_nad_bound_m
      use lagp_nad_bound_m

      use gyre_nad_trans_m
      use dziem_nad_trans_m
      use jcd_nad_trans_m
      use rjcd_nad_trans_m
      use mix_nad_trans_m
      use lagp_nad_trans_m

      use nad_diff_m

      class(context_t), pointer, intent(in) :: cx
      type(grid_t), intent(in)              :: gr
      type(mode_par_t), intent(in)          :: md_p
      type(num_par_t), intent(in)           :: nm_p
      type(osc_par_t), intent(in)           :: os_p
      type(nad_bvp_t)                       :: bp

      class(bound_ct), allocatable       :: bd
      integer                            :: j
      type(diff_element_ct), allocatable :: de(:)

      ! Construct the nad_bvp_t

      ! Initialize the boundary conditions

      select case(os_p%variables_set)
      case('GYRE')
         bd = gyre_nad_bound_t(cx, os_p)
      case('DZIEM')
         bd = dziem_nad_bound_t(cx, os_p)
      case('JCD')
         if (cx%l == 0) then
            bd = rjcd_nad_bound_t(cx, os_p)
         else
            bd = jcd_nad_bound_t(cx, os_p)
         end if
      case('MIX')
         bd = mix_nad_bound_t(cx, os_p)
      case('LAGP')
         bd = lagp_nad_bound_t(cx, os_p)
      case default
         @:ABORT('invalid variables_set')
      end select

      ! Initialize the difference equations

      allocate(de(gr%n-1))

      !$OMP PARALLEL DO
      do j = 1, gr%n-1
         allocate(de(j)%df, SOURCE=nad_diff_t(cx, gr%pt(j), gr%pt(j+1), nm_p, os_p))
      end do

      ! Initialize the bvp_t

      bp%bvp_ct = bvp_ct(bd, de, nm_p)

      ! Other initializations

      bp%cx => cx
      bp%gr = gr

      select case(os_p%variables_set)
      case('GYRE')
         allocate(gyre_nad_trans_t::bp%tr(gr%n))
      case('DZIEM')
         allocate(dziem_nad_trans_t::bp%tr(gr%n))
      case('JCD')
         if (md_p%l == 0) then
            allocate(rjcd_nad_trans_t::bp%tr(gr%n))
         else
            allocate(jcd_nad_trans_t::bp%tr(gr%n))
         end if
      case('MIX')
         allocate(mix_nad_trans_t::bp%tr(gr%n))
      case('LAGP')
         allocate(lagp_nad_trans_t::bp%tr(gr%n))
      case default
         @:ABORT('invalid variables_set')
      end select

      !$OMP PARALLEL DO
      do j = 1, gr%n

         select type(tr => bp%tr(j))
         type is(gyre_nad_trans_t)
            tr = gyre_nad_trans_t(cx, gr%pt(j))
         type is(dziem_nad_trans_t)
            tr = dziem_nad_trans_t(cx, gr%pt(j))
         type is(jcd_nad_trans_t)
            tr = jcd_nad_trans_t(cx, gr%pt(j))
         type is(rjcd_nad_trans_t)
            tr = rjcd_nad_trans_t(cx, gr%pt(j))
         type is(mix_nad_trans_t)
            tr = mix_nad_trans_t(cx, gr%pt(j))
         type is(lagp_nad_trans_t)
            tr = lagp_nad_trans_t(cx, gr%pt(j))
         class default
            @:ABORT('invalid type')
         end select

      end do

      bp%md_p = md_p
      bp%nm_p = nm_p
      bp%os_p = os_p

      ! Finish

      return

   end function nad_bvp_t_

   !****

   function wave_t_hom_(bp, sm, st, id, chi, n_iter) result(wv)

      class(nad_bvp_t), intent(in)    :: bp
      class(sysmtx_ct), intent(inout) :: sm
      type(state_ct), intent(in)      :: st
      integer, intent(in)             :: id
      real(RD), intent(in)            :: chi
      integer, intent(in)             :: n_iter
      type(wave_t)                    :: wv

      complex(RD) :: y(6,bp%n)
      integer     :: j

      ! Calculate the homogeneous solution vector

      call bp%solve_hom(sm, st, y)

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr(j)%transform(st, y(:,j))
      end do

      ! Construct the wave_t

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_hom_

   !****

   function wave_t_inhom_(bp, sm, st, z_i, z_o, id, chi, n_iter) result(wv)

      class(nad_bvp_t), intent(in)    :: bp
      class(sysmtx_ct), intent(inout) :: sm
      type(state_ct), intent(in)      :: st
      complex(RD), intent(in)         :: z_i(:)
      complex(RD), intent(in)         :: z_o(:)
      integer, intent(in)             :: id
      real(RD), intent(in)            :: chi
      integer, intent(in)             :: n_iter
      type(wave_t)                    :: wv

      complex(RD) :: y(6,bp%n)
      integer     :: j

      @:CHECK_BOUNDS(SIZE(z_i), bp%n_i)
      @:CHECK_BOUNDS(SIZE(z_o), bp%n_o)

      ! Calculate the inhomogeneous solution vector

      call bp%solve_inhom(sm, st, z_i, z_o, y)

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr(j)%transform(st, y(:,j))
      end do

      ! Construct the wave_t

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_inhom_

   !****

   function wave_t_y_(bp, st, y, id, chi, n_iter) result(wv)

      class(nad_bvp_t), intent(in) :: bp
      type(state_ct), intent(in)   :: st
      complex(RD), intent(in)      :: y(:,:)
      integer, intent(in)          :: id
      real(RD), intent(in)         :: chi
      integer, intent(in)          :: n_iter
      type(wave_t)                 :: wv

      @:CHECK_BOUNDS(SIZE(y, 1), bp%n_e)
      @:CHECK_BOUNDS(SIZE(y, 2), bp%n)

      ! Construct the wave_t

      wv = wave_t(st, y, bp%cx, bp%gr, bp%md_p, bp%nm_p, bp%os_p, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_y_

end module nad_bvp_m
