! Module  : nad_eqns_m
! Purpose : nonadiabatic differential equations
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

#:set P = os.path.basename(_FILE_).split('_')[0]

module ${P}$_nad_eqns_m

   ! Uses

   use forum_m, only: RD

   use context_m
   use eqns_m
   use linalg_m
   use math_m
   use model_m
   use model_util_m
   use osc_par_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(eqns_ct) :: ${P}$_nad_eqns_t
      private
      type(context_t), pointer :: cx => null()
      type(point_t)            :: pt
      real(RD)                 :: V_2
      real(RD)                 :: dV_2
      real(RD)                 :: V
      real(RD)                 :: V_g
      real(RD)                 :: As
      real(RD)                 :: As_iso
      real(RD)                 :: U
      real(RD)                 :: c_1
      real(RD)                 :: Gamma_1
      real(RD)                 :: ups_T
      real(RD)                 :: nabla_ad
      real(RD)                 :: nabla
      real(RD)                 :: c_rad
      real(RD)                 :: dc_rad
      real(RD)                 :: c_thn
      real(RD)                 :: dc_thn
      real(RD)                 :: c_thk
      real(RD)                 :: c_eps
      real(RD)                 :: c_egv
      complex(RD)              :: c_eps_ad
      complex(RD)              :: c_eps_S
      real(RD)                 :: c_kap_ad
      real(RD)                 :: c_kap_S
      real(RD)                 :: c_dif
      real(RD)                 :: f_conv
      real(RD)                 :: Omega_rot
      real(RD)                 :: x
      complex(RD)              :: lambda
      complex(RD)              :: l_i
   contains
      private
      procedure, public :: eval_A
      procedure, public :: eval_C
   end type ${P}$_nad_eqns_t

   ! Interfaces

   interface ${P}$_nad_eqns_t
      module procedure ${P}$_nad_eqns_t_
   end interface ${P}$_nad_eqns_t

   ! Access specifiers

   public :: ${P}$_nad_eqns_t

   ! Default access

   private

contains

   function ${P}$_nad_eqns_t_(cx, pt, os_p) result(eq)

      type(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)            :: pt
      type(osc_par_t), intent(in)          :: os_p
      type(${P}$_nad_eqns_t)               :: eq

      class(model_t), pointer :: ml
      complex(RD)             :: eps_rho
      complex(RD)             :: eps_T

      ! Construct the ${P}$_nad_eqns_t

      eq%cx => cx

      eq%pt = pt

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [ &
         I_V_2,I_AS,I_U,I_C_1,I_GAMMA_1,I_NABLA,I_NABLA_AD,I_UPS_T, &
         I_C_LUM,I_C_RAD,I_C_THN,I_C_THK,I_C_EPS,I_C_EGV, &
         I_KAP_RHO,I_KAP_T])

      @:ASSERT(.NOT. ml%is_vacuum(pt), 'vacuum point')

      eq%V_2 = ml%coeff(I_V_2, pt)
      eq%dV_2 = ml%dcoeff(I_V_2, pt)
      eq%V = eq%V_2*pt%x**2
      eq%V_g = eq%V/ml%coeff(I_GAMMA_1, pt)
      eq%As = ml%coeff(I_AS, pt)
      eq%As_iso = eq%As*MERGE(MERGE(cx%alpha_pi, cx%alpha_gam, pt%x<cx%x_atm), 1._RD, eq%As > 0._RD)
      eq%U = ml%coeff(I_U, pt)
      eq%c_1 = ml%coeff(I_C_1, pt)
      eq%Gamma_1 = ml%coeff(I_GAMMA_1, pt)
      eq%nabla_ad = ml%coeff(I_NABLA_AD, pt)
      eq%nabla = ml%coeff(I_NABLA, pt)
      eq%ups_T = ml%coeff(I_UPS_T, pt)
      eq%c_rad = ml%coeff(I_C_RAD, pt)
      eq%dc_rad = ml%dcoeff(I_C_RAD, pt)
      eq%c_thn = ml%coeff(I_C_THN, pt)
      eq%dc_thn = ml%dcoeff(I_C_THN, pt)
      eq%c_thk = ml%coeff(I_C_THK, pt)
      eq%c_eps = ml%coeff(I_C_EPS, pt)
      eq%c_egv = ml%coeff(I_C_EGV, pt)

      eq%c_kap_ad = eq%nabla_ad*cx%alpha_kat*ml%coeff(I_KAP_T, pt) + cx%alpha_kar*ml%coeff(I_KAP_RHO, pt)/eq%Gamma_1
      eq%c_kap_S = cx%alpha_kat*ml%coeff(I_KAP_T, pt) - eq%ups_T*cx%alpha_kar*ml%coeff(I_KAP_RHO, pt)

      eq%c_dif = -4._RD*eq%nabla_ad*eq%V*eq%nabla + eq%nabla_ad*(ml%dcoeff(I_NABLA_AD, pt) + eq%V)

      select case (os_p%conv_scheme)
      case ('FROZEN_PESNELL_1')
         eq%f_conv = eq%c_rad*(3._RD + eq%dc_rad)
      case ('FROZEN_PESNELL_4')
         eq%f_conv = ml%coeff(I_C_LUM, pt)*(3._RD + ml%dcoeff(I_C_LUM, pt)) - (ml%coeff(I_C_LUM, pt) - eq%c_rad)
      case default
         @:ABORT('invalid conv_scheme')
      end select

      eq%Omega_rot = cx%Omega_rot(pt)

      if (cx%angular_cache) then
         eq%lambda = cx%lambda(eq%Omega_rot, state_ct(0._RD))
         eq%l_i = cx%l_e(cx%Omega_rot_i, state_ct(0._RD))
      end if

      if (cx%epsilon_cache) then
         eps_rho = cx%eps_rho(state_ct(0._RD), pt)
         eps_T = cx%eps_T(state_ct(0._RD), pt)
         eq%c_eps_ad = eq%c_eps*(eq%nabla_ad*eps_T + eps_rho/eq%Gamma_1)
         eq%c_eps_S = eq%c_eps*(eps_T - eq%ups_T*eps_rho)
      end if

      ! Other initializations

      eq%x = pt%x

      eq%n_e = 6

      ! Finish

      return

   end function ${P}$_nad_eqns_t_

   !****

   subroutine eval_A(this, st, A)

      class(${P}$_nad_eqns_t), intent(in) :: this
      class(state_ct), intent(in)         :: st
      complex(RD), intent(out)            :: A(:,:)

      complex(RD) :: omega_c
      complex(RD) :: i_omega_c
      complex(RD) :: lambda
      complex(RD) :: l_i
      complex(RD) :: om2
      complex(RD) :: eta
      complex(RD) :: f_rht
      complex(RD) :: df_rht
      complex(RD) :: eps_rho
      complex(RD) :: eps_T
      complex(RD) :: c_eps_ad
      complex(RD) :: c_eps_S
      real(RD)    :: invx

      @:CHECK_BOUNDS(SIZE(A, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(A, 2), this%n_e)

      ! Evaluate the Jacobian matrix

      associate ( &
         omega => st%omega, &
         V_2 => this%V_2, &
         dV_2 => this%dV_2, &
         V => this%V, &
         V_g => this%V_g, &
         As => this%As, &
         As_iso => this%As_iso, &
         U => this%U, &
         c_1 => this%c_1, &
         Gamma_1 => this%Gamma_1, &
         nabla_ad => this%nabla_ad, &
         nabla => this%nabla, &
         ups_T => this%ups_T, &
         c_rad => this%c_rad, &
         dc_rad => this%dc_rad, &
         c_thn => this%c_thn, &
         dc_thn => this%dc_thn, &
         c_thk => this%c_thk, &
         c_eps => this%c_eps, &
         c_egv => this%c_egv, &
         c_kap_ad => this%c_kap_ad, &
         c_kap_S => this%c_kap_S, &
         c_dif => this%c_dif, &
         f_conv => this%f_conv, &
         Omega_rot => this%Omega_rot, &
         Omega_rot_i => this%cx%Omega_rot_i, &
         x => this%x, &
         pt => this%pt, &
         alpha_grv => this%cx%alpha_grv, &
         alpha_thm => this%cx%alpha_thm, &
         alpha_hfl => this%cx%alpha_hfl, &
         alpha_rht => this%cx%alpha_rht, &
         alpha_omg => this%cx%alpha_omg, &
         alpha_gam => this%cx%alpha_gam, &
         alpha_pi => this%cx%alpha_pi, &
         alpha_egv => this%cx%alpha_egv, &
         m => this%cx%m, &
         angular_cache => this%cx%angular_cache, &
         epsilon_cache => this%cx%epsilon_cache)

         omega_c = omega - m*Omega_rot

         i_omega_c = (0._RD,1._RD)*sqrt(CMPLX(alpha_omg, KIND=RD))*omega_c

         if (angular_cache) then
            lambda = this%lambda
            l_i = this%l_i
         else
            lambda = this%cx%lambda(Omega_rot, st)
            l_i = this%cx%l_e(Omega_rot_i, st)
         end if

         om2 = c_1*alpha_omg*omega_c**2
         eta = lambda/om2

         f_rht = 1._RD - 0.25_RD*alpha_rht*i_omega_c*c_thn
         df_rht = -0.25_RD*alpha_rht*i_omega_c*c_thn*dc_thn/f_rht

         if (epsilon_cache) then
            c_eps_ad = this%c_eps_ad
            c_eps_S = this%c_eps_S
         else
            eps_rho = this%cx%eps_rho(st, pt)
            eps_T = this%cx%eps_T(st, pt)
            c_eps_ad = c_eps*(nabla_ad*eps_T + eps_rho/Gamma_1)
            c_eps_S = c_eps*(eps_T - ups_T*eps_rho)
         end if

         invx = 1._RD/x

         ! Set up the matrix

         #:include 'A.inc'

      end associate

      ! Finish

      return

   end subroutine eval_A

   !****

   subroutine eval_C(this, st, C)

      class(${P}$_nad_eqns_t), intent(in) :: this
      class(state_ct), intent(in)         :: st
      complex(RD), intent(out)            :: C(:,:)

      complex(RD) :: omega_c
      complex(RD) :: lambda

      @:CHECK_BOUNDS(SIZE(C, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(C, 2), this%n_e)

      ! Evaluate the match matrix

      associate ( &
         omega => st%omega, &
         V_2 => this%V_2, &
         dV_2 => this%dV_2, &
         V => this%V, &
         U => this%U, &
         c_1 => this%c_1, &
         nabla_ad => this%nabla_ad, &
         Omega_rot => this%Omega_rot, &
         alpha_omg => this%cx%alpha_omg, &
         m => this%cx%m, &
         angular_cache => this%cx%angular_cache)

         omega_c = omega - m*Omega_rot

         if (angular_cache) then
            lambda = this%lambda
         else
            lambda = this%cx%lambda(Omega_rot, st)
         end if

         ! Set up the matrix

         #:include 'C.inc'

      end associate

      ! Finish

      return

   end subroutine eval_C

end module ${P}$_nad_eqns_m
