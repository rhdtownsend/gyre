! Module  : angular_factory_m
! Purpose : factory procedures for angular_rt and angular_ct types
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module angular_factory_m

   ! Uses

   use forum_m, only: RD

   use adhoc_angular_m
   use angular_m
   use mode_par_m
   use osc_par_m
   use sph_angular_m
   use tar_angular_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   #:for S in DTYPE_SUFFIXES

      ! Interfaces

      interface angular_${S}$
         module procedure angular_${S}$_
      end interface angular_${S}$

      ! Access specifiers

      public :: angular_${S}$

   #:endfor

   ! Default access

   private

contains

   #:for T, S in zip(DTYPE_BASE_TYPES, DTYPE_SUFFIXES)

      function angular_${S}$_(md_p, os_p) result(an)

         type(mode_par_t), intent(in)  :: md_p
         type(osc_par_t), intent(in)   :: os_p
         class(angular_${S}$), allocatable :: an

         ! Create a rot_t type

         select case (os_p%lambda_method)
         case ('SPH')
            allocate(an, SOURCE=sph_angular_${S}$(md_p))
         case ('TAR-GRAVITY')
            allocate(an, SOURCE=tar_angular_${S}$(md_p, rossby=.FALSE.))
         case ('TAR-ROSSBY')
            allocate(an, SOURCE=tar_angular_${S}$(md_p, rossby=.TRUE.))
         case ('ADHOC')
            allocate(an, SOURCE=adhoc_angular_${S}$(os_p%lambda))
         case default
            @:ABORT('invalid lambda_method')
         end select

         ! Finish

         return

      end function angular_${S}$_

   #:endfor

end module angular_factory_m
