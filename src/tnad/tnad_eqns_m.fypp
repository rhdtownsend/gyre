! Module  : tnad_eqns_m
! Purpose : nonadiabatic (+turbulent convection) differential equations
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module tnad_eqns_m

   ! Uses

   use forum_m, only: RD

   use context_m
   use eqns_m
   use linalg_m
   use math_m
   use model_m
   use mode_par_m
   use model_util_m
   use nad_eqns_m
   use nad_trans_m
   use osc_par_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(eqns_ct) :: tnad_eqns_t
      private
      type(nad_eqns_t)         :: eq
      type(context_t), pointer :: cx => null()
      type(nad_trans_t)        :: tr
      real(RD)                 :: V
      real(RD)                 :: V_g
      real(RD)                 :: As
      real(RD)                 :: c_1
      real(RD)                 :: Omega_rot
      real(RD)                 :: x
   contains
      private
      procedure, public :: eval_A
      procedure, public :: y_trb
      procedure, public :: F_trb
   end type tnad_eqns_t

   ! Interfaces

   interface tnad_eqns_t
      module procedure tnad_eqns_t_
   end interface tnad_eqns_t

   ! Access specifiers

   public :: tnad_eqns_t

   ! Default access

   private

contains

   function tnad_eqns_t_(cx, pt, md_p, os_p) result(eq)

      type(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)            :: pt
      type(mode_par_t), intent(in)         :: md_p
      type(osc_par_t), intent(in)          :: os_p
      type(tnad_eqns_t)                    :: eq

      type(osc_par_t)         :: os_p_eq
      class(model_t), pointer :: ml

      ! Construct the tnad_eqns_t, as a decorator around nad_eqns_t

      os_p_eq = os_p
      os_p_eq%variables_set = 'GYRE'

      eq%eq = nad_eqns_t(cx, pt, md_p, os_p_eq)

      eq%cx => cx

      eq%tr = nad_trans_t(cx, pt, md_p, os_p)

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [I_V_2,I_AS,I_C_1,I_GAMMA_1,I_OMEGA_ROT])

      @:ASSERT(.NOT. ml%is_vacuum(pt), 'vacuum point')

      eq%V = ml%coeff(I_V_2, pt)*pt%x**2
      eq%V_g = ml%coeff(I_V_2, pt)*pt%x**2/ml%coeff(I_GAMMA_1, pt)
      eq%As = ml%coeff(I_AS, pt)
      eq%c_1 = ml%coeff(I_C_1, pt)

      eq%Omega_rot = cx%Omega_rot(pt)

      ! Other initializations

      eq%x = pt%x

      eq%n_e = 6

      ! Finish

      return

   end function tnad_eqns_t_

   !****

   subroutine eval_A(this, st, A)

      class(tnad_eqns_t), intent(in) :: this
      class(state_ct), intent(in)    :: st
      complex(RD), intent(out)       :: A(:,:)

      real(RD)    :: Omega_rot_i
      complex(RD) :: l_i
      complex(RD) :: F_trb
      real(RD)    :: invx
      complex(RD) :: B(6,1)
      complex(RD) :: C(1,6)
      complex(RD) :: D

      @:CHECK_BOUNDS(SIZE(A, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(A, 2), this%n_e)

      ! Evaluate the RHS matrix

      associate ( &
         V_g => this%V_g, &
         x => this%x, &
         Omega_rot_i => this%cx%Omega_rot_i)

         ! Set up original (nad) eqns

         call this%eq%eval_A(st, A)

         ! Build matrices to add turbulent correction to y_2 (through a
         ! virtual y_trb variable)
         !
         !  dy/dx = A*y + B*y_trb
         !      0 = C*y + D*y_trb

         l_i = this%cx%l_e(Omega_rot_i, st)

         F_trb = this%F_trb(st)

         invx = 1._RD/x

         B(1,1) = -A(1,2)
         B(2,1) = invx*(V_g - 2._RD)
         B(3,1) = -A(3,2)
         B(4,1) = -A(4,2)
         B(5,1) = -A(5,2)
         B(6,1) = -A(6,2)

         C(1,1) = F_trb*(invx*(l_i - 1._RD) + A(1,1))
         C(1,2) = F_trb*A(1,2)
         C(1,3) = F_trb*A(1,3)
         C(1,4) = F_trb*A(1,4)
         C(1,5) = F_trb*A(1,5)
         C(1,6) = F_trb*A(1,6)

         D = - invx*1._RD - F_trb*A(1,2)

         ! Eliminate y_trb to update A

         A = A - MATMUL(B, C)/D

         ! Apply the variables transformation

         call this%tr%trans_eqns(A, st)

      end associate

      ! Finish

      return

   end subroutine eval_A

   !****

   function y_trb(this, st, y)

      class(tnad_eqns_t), intent(inout) :: this
      class(state_ct), intent(in)       :: st
      complex(RD), intent(in)           :: y(:)
      complex(RD)                       :: y_trb

      complex(RD) :: A(6,6)
      complex(RD) :: l_i
      complex(RD) :: F_trb
      complex(RD) :: C(6)
      complex(RD) :: D

      @:CHECK_BOUNDS(SIZE(y), 6)

      ! Evaluate the turbulent force variable y_trb from
      ! the solution vector

      associate ( &
         Omega_rot_i => this%cx%Omega_rot_i)

         ! Set up original (nad) eqns

         call this%eq%eval_A(st, A)

         ! Evaluate y_trb

         l_i = this%cx%l_e(Omega_rot_i, st)

         F_trb = this%F_trb(st)

         C(1) = F_trb*((l_i - 1._RD) + A(1,1))
         C(2) = F_trb*A(1,2)
         C(3) = F_trb*A(1,3)
         C(4) = F_trb*A(1,4)
         C(5) = F_trb*A(1,5)
         C(6) = F_trb*A(1,6)

         D = - 1._RD - F_trb*A(1,2)

         y_trb = -DOT_PRODUCT(C, y)/D

      end associate

      ! Finish

      return

   end function y_trb

   !****

   function F_trb(this, st)

      class(tnad_eqns_t), intent(in) :: this
      class(state_ct), intent(in)    :: st
      complex(RD)                    :: F_trb

      complex(RD) :: omega_c
      real(RD)    :: omega_c_r
      complex(RD) :: i_omega_c
      complex(RD) :: l_i
      real(RD)    :: H_r
      real(RD)    :: tau_conv
      complex(RD) :: nu_trb

      ! Evaluate the turbulent viscosity factor i*sigma*nu/(g r)

      associate ( &
         omega => st%omega, &
         omega_r => st%omega_r, &
         V => this%V, &
         As => this%As, &
         c_1 => this%c_1, &
         Omega_rot => this%Omega_rot, &
         x => this%x, &
         Omega_rot_i => this%cx%Omega_rot_i, &
         alpha_omg => this%cx%alpha_omg, &
         alpha_trb => this%cx%alpha_trb, &
         m => this%cx%m)

         if (x > 0._RD .AND. As < 0._RD) then

            omega_c = omega - m*Omega_rot
            omega_c_r = omega_r - m*Omega_rot

            i_omega_c = (0._RD,1._RD)*sqrt(CMPLX(alpha_omg, KIND=RD))*omega_c

            l_i = this%cx%l_e(Omega_rot_i, st)

            H_r = MIN(1._RD/V, 1._RD)  ! = H_P/r
            tau_conv = SQRT(-c_1/As)

            nu_trb = c_1*(H_r*alpha_trb)**2/(tau_conv*(1._RD + tau_conv*ABS(omega_c_r)/TWOPI))

            F_trb = i_omega_c*nu_trb

         else

            F_trb = 0._RD

         endif

      end associate

      ! Finish

      return

   end function F_trb

end module tnad_eqns_m
