! Module  : rad_bound_m
! Purpose : adiabatic radial boundary conditions
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

#:set P = os.path.basename(_FILE_).split('_')[0]

module ${P}$_rad_bound_m

   ! Uses

   use forum_m, only: RD

   use atmos_m
   use bound_m
   use context_m
   use model_m
   use model_util_m
   use osc_par_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Parameter definitions

   integer, parameter :: REGULAR_TYPE = 1
   integer, parameter :: ZERO_R_TYPE = 2
   integer, parameter :: VACUUM_TYPE = 3
   integer, parameter :: DZIEM_TYPE = 4
   integer, parameter :: ISOTHRM_TYPE = 5
   integer, parameter :: UNNO_TYPE = 6
   integer, parameter :: JCD_TYPE = 7

   ! Derived-type definitions

   type, extends(bound_rt) :: ${P}$_rad_bound_t
      private
      type(context_t), pointer   :: cx => null()
      real(RD)                   :: V_2_i
      real(RD)                   :: V_o
      real(RD)                   :: V_2_o
      real(RD)                   :: V_g_o
      real(RD)                   :: As_o
      real(RD)                   :: U_o
      real(RD)                   :: c_1_i
      real(RD)                   :: c_1_o
      procedure(build_), pointer :: build_i_
      procedure(build_), pointer :: build_o_
   contains
      private
      procedure, public :: build_i
      procedure, public :: build_o
   end type ${P}$_rad_bound_t

   ! Interfaces

   interface
      subroutine build_(this, st, B)
         use forum_m, only: RD
         use state_m
         import ${P}$_rad_bound_t
         class(${P}$_rad_bound_t), intent(in) :: this
         class(state_rt), intent(in)          :: st
         real(RD), intent(out)                :: B(:,:)
      end subroutine build_
   end interface

   interface ${P}$_rad_bound_t
      module procedure ${P}$_rad_bound_t_
   end interface ${P}$_rad_bound_t

   ! Access specifiers

   public :: ${P}$_rad_bound_t

   ! Default access

   private

contains

   function ${P}$_rad_bound_t_(cx, os_p) result(bd)

      type(context_t), pointer, intent(in) :: cx
      type(osc_par_t), intent(in)          :: os_p
      type(${P}$_rad_bound_t)              :: bd

      class(model_t), pointer :: ml
      type(point_t)           :: pt_i
      type(point_t)           :: pt_o

      ! Construct the ${P}$_rad_bound_t

      ml => cx%model()
      pt_i = cx%point_i()
      pt_o = cx%point_o()

      select case (os_p%inner_bound)
      case ('REGULAR')
         @:ASSERT(pt_i%x == 0._RD, 'boundary condition invalid for x /= 0')
         bd%build_i_ => build_i_regular_
      case ('ZERO_R')
         @:ASSERT(pt_i%x /= 0._RD, 'boundary condition invalid for x == 0')
         bd%build_i_ => build_i_zero_r_
      case default
         @:ABORT('invalid inner_bound')
      end select

      select case (os_p%outer_bound)
      case ('VACUUM')
         bd%build_o_ => build_o_vacuum_
      case ('ZERO_R')
         bd%build_o_ => build_o_zero_r_
      case ('DZIEM')
         if (ml%is_vacuum(pt_o)) then
            bd%build_o_ => build_o_vacuum_
         else
            bd%build_o_ => build_o_dziem_
         end if
      case ('ISOTHERMAL')
         if (ml%is_vacuum(pt_o)) then
            bd%build_o_ => build_o_vacuum_
         else
            bd%build_o_ => build_o_decomp_
         end if
      case ('UNNO')
         if (ml%is_vacuum(pt_o)) then
            bd%build_o_ => build_o_vacuum_
         else
            bd%build_o_ => build_o_decomp_
         end if
      case ('JCD')
         if (ml%is_vacuum(pt_o)) then
            bd%build_o_ => build_o_vacuum_
         else
            bd%build_o_ => build_o_jcd_
         end if
      case default
         @:ABORT('invalid outer_bound')
      end select

      bd%cx => cx

      ! Store structure coefficients

      call check_model(ml, [I_V_2,I_U,I_C_1,I_GAMMA_1])

      bd%c_1_i = ml%coeff(I_C_1, pt_i)
      bd%V_2_i = ml%coeff(I_C_1, pt_i)

      select case (os_p%outer_bound)
      case ('VACUUM', 'ZERO_R')
         bd%V_o = ml%coeff(I_V_2, pt_o)*pt_o%x**2
      case ('DZIEM')
         bd%V_o = ml%coeff(I_V_2, pt_o)*pt_o%x**2
      case ('ISOTHERMAL')
          call eval_atmos_coeffs_isothrm(ml, pt_o, bd%V_g_o, &
            bd%As_o, bd%c_1_o)
      case ('UNNO')
         call eval_atmos_coeffs_unno(ml, pt_o, bd%V_g_o, &
            bd%As_o, bd%c_1_o)
      case ('JCD')
         call eval_atmos_coeffs_isothrm(ml, pt_o, bd%V_g_o, &
            bd%As_o, bd%c_1_o)
      case default
         @:ABORT('invalid outer_bound')
      end select

      bd%V_2_o = ml%coeff(I_V_2, pt_o)
      bd%U_o = ml%coeff(I_U, pt_o)
      bd%c_1_o = ml%coeff(I_C_1, pt_o)

      ! Other initializations

      bd%n_i = 1
      bd%n_o = 1

      bd%n_e = 2

      ! Finish

      return

   end function ${P}$_rad_bound_t_

   !****

   subroutine build_i(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      ! Evaluate the inner boundary conditions

      call this%build_i_(st, B)

      ! Finish

      return

   end subroutine build_i

   !****

   subroutine build_i_regular_(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      real(RD) :: omega_c

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_i)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the inner boundary conditions (regular-enforcing)

      associate( &
         omega => st%omega, &
         V_2 => this%V_2_i, &
         c_1 => this%c_1_i, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Set up the matrix

         #:include 'B_i_regular.inc'

      end associate

      ! Finish

      return

   end subroutine build_i_regular_

   !****

   subroutine build_i_zero_r_(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_i)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the inner boundary conditions (zero radial
      ! displacement)

      ! Set up the matrix

      #:include 'B_i_zero_r.inc'

      ! Finish

      return

   end subroutine build_i_zero_r_

   !****

   subroutine build_o(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      ! Evaluate the outer boundary conditions

      call this%build_o_(st, B)

      ! Finish

      return

   end subroutine build_o

   !****

   subroutine build_o_vacuum_(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      real(RD) :: omega_c

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions (vacuum)

      associate( &
         omega => st%omega, &
         V_2 => this%V_2_o, &
         U => this%U_o, &
         c_1 => this%c_1_o, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Set up the matrix

         #:include 'B_o_vacuum.inc'

      end associate

      ! Finish

      return

   end subroutine build_o_vacuum_

   !****

   subroutine build_o_zero_r_(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions (zero radial
      ! displacement)

      associate( &
         V_2 => this%V_2_o)

         ! Set up the matrix

         #:include 'B_o_zero_r.inc'

      end associate

      ! Finish

      return

   end subroutine build_o_zero_r_

   !****

   subroutine build_o_dziem_(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      real(RD) :: omega_c

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions ([Dzi1971] formulation)

      associate( &
         omega => st%omega, &
         V_2 => this%V_2_o, &
         V => this%V_o, &
         c_1 => this%c_1_o, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Set up the matrix

         #:include 'B_o_dziem.inc'

      end associate

      ! Finish

      return

   end subroutine build_o_dziem_

   !****

   subroutine build_o_decomp_(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      real(RD) :: omega_c
      real(RD) :: a_11
      real(RD) :: a_12
      real(RD) :: chi

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions, based on a local
      ! eigendecomposition

      associate( &
         omega => st%omega, &
         V_2 => this%V_2_o, &
         V_g => this%V_g_o, &
         As => this%As_o, &
         c_1 => this%c_1_o, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Evaluate selected elements of the Jacobian matrix

         a_11 = V_g - 3._RD
         a_12 = -V_g

         ! Evaluate the eigenvalue for the wave we want to keep

         chi = atmos_chi(V_g, As, c_1, alpha_omg, omega_c, 0._RD)

         ! Set up the matrix

         #:include 'B_o_decomp.inc'

      end associate

      ! Finish

      return

   end subroutine build_o_decomp_

   !****

   subroutine build_o_jcd_(this, st, B)

      class(${P}$_rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(out)                :: B(:,:)

      real(RD) :: omega_c
      real(RD) :: chi
      real(RD) :: b_11
      real(RD) :: b_12

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions ([Chr2008] formulation)

      ! Calculate coefficients

      associate( &
         omega => st%omega, &
         V_2 => this%V_2_o, &
         V_g => this%V_g_o, &
         As => this%As_o, &
         c_1 => this%c_1_o, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         chi = atmos_chi(V_g, As, c_1, alpha_omg, omega_c, 0._RD)

         b_11 = V_g - 3._RD
         b_12 = -V_g

         ! Set up the boundary conditions

         #:include 'B_o_jcd.inc'
         B(1,2) = -b_12

      end associate

      ! Finish

      return

   end subroutine build_o_jcd_

end module ${P}$_rad_bound_m
