! Module  : rad_bound_m
! Purpose : adiabatic radial boundary conditions
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module rad_bound_m

   ! Uses

   use forum_m, only: RD

   use atmos_m
   use bound_m
   use context_m
   use model_m
   use model_util_m
   use mode_par_m
   use osc_par_m
   use point_m
   use rad_trans_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Parameter definitions

   integer, parameter :: REGULAR_TYPE = 1
   integer, parameter :: ZERO_R_TYPE = 2
   integer, parameter :: VACUUM_TYPE = 3
   integer, parameter :: DZIEM_TYPE = 4
   integer, parameter :: ISOTHRM_TYPE = 5
   integer, parameter :: UNNO_TYPE = 6
   integer, parameter :: JCD_TYPE = 7

   ! Derived-type definitions

   type, extends(bound_rt) :: rad_bound_t
      private
      type(context_t), pointer :: cx => null()
      type(rad_trans_t)        :: tr_i
      type(rad_trans_t)        :: tr_o
      type(point_t)            :: pt_i
      type(point_t)            :: pt_o
      real(RD)                 :: V_o
      real(RD)                 :: V_g_o
      real(RD)                 :: As_o
      real(RD)                 :: U_o
      real(RD)                 :: c_1_i
      real(RD)                 :: c_1_o
      integer                  :: type_i
      integer                  :: type_o
   contains
      private
      procedure, public :: build_i
      procedure         :: build_regular_i_
      procedure         :: build_zero_r_i_
      procedure, public :: build_o
      procedure         :: build_vacuum_o_
      procedure         :: build_zero_r_o_
      procedure         :: build_dziem_o_
      procedure         :: build_decomp_o_
      procedure         :: build_jcd_o_
   end type rad_bound_t

   ! Interfaces

   interface rad_bound_t
      module procedure rad_bound_t_
   end interface rad_bound_t

   ! Access specifiers

   public :: rad_bound_t

   ! Default access

   private

contains

   function rad_bound_t_(cx, md_p, os_p) result(bd)

      type(context_t), pointer, intent(in) :: cx
      type(mode_par_t), intent(in)         :: md_p
      type(osc_par_t), intent(in)          :: os_p
      type(rad_bound_t)                    :: bd

      class(model_t), pointer :: ml
      type(point_t)           :: pt_i
      type(point_t)           :: pt_o

      ! Construct the rad_bound_t

      ml => cx%model()
      pt_i = cx%point_i()
      pt_o = cx%point_o()

      select case (os_p%inner_bound)
      case ('REGULAR')
         @:ASSERT(pt_i%x == 0._RD, 'boundary condition invalid for x /= 0')
         bd%type_i = REGULAR_TYPE
      case ('ZERO_R')
         @:ASSERT(pt_i%x /= 0._RD, 'boundary condition invalid for x == 0')
         bd%type_i = ZERO_R_TYPE
      case default
         @:ABORT('invalid inner_bound')
      end select

      select case (os_p%outer_bound)
      case ('VACUUM')
         bd%type_o = VACUUM_TYPE
      case ('ZERO_R')
         bd%type_o = ZERO_R_TYPE
      case ('DZIEM')
         if (ml%is_vacuum(pt_o)) then
            bd%type_o = VACUUM_TYPE
         else
            bd%type_o = DZIEM_TYPE
         endif
      case ('ISOTHERMAL')
         if (ml%is_vacuum(pt_o)) then
            bd%type_o = VACUUM_TYPE
         else
            bd%type_o = ISOTHRM_TYPE
         endif
      case ('UNNO')
         if (ml%is_vacuum(pt_o)) then
            bd%type_o = VACUUM_TYPE
         else
            bd%type_o = UNNO_TYPE
         endif
      case ('JCD')
         if (ml%is_vacuum(pt_o)) then
            bd%type_o = VACUUM_TYPE
         else
            bd%type_o = JCD_TYPE
         endif
      case default
         @:ABORT('invalid outer_bound')
      end select

      bd%cx => cx

      bd%tr_i = rad_trans_t(cx, pt_i, md_p, os_p)
      bd%tr_o = rad_trans_t(cx, pt_o, md_p, os_p)

      ! Store structure coefficients

      ! Store structore coefficients

      call check_model(ml, [I_V_2,I_U,I_C_1,I_GAMMA_1,I_OMEGA_ROT])

      bd%c_1_i = ml%coeff(I_C_1, pt_i)

      select case (bd%type_o)
      case (VACUUM_TYPE,ZERO_R_TYPE)
         bd%V_o = ml%coeff(I_V_2, pt_o)*pt_o%x**2
      case (DZIEM_TYPE)
         bd%V_o = ml%coeff(I_V_2, pt_o)*pt_o%x**2
      case (ISOTHRM_TYPE)
          call eval_atmos_coeffs_isothrm(ml, pt_o, bd%V_g_o, &
            bd%As_o, bd%c_1_o)
      case (UNNO_TYPE)
         call eval_atmos_coeffs_unno(ml, pt_o, bd%V_g_o, &
            bd%As_o, bd%c_1_o)
      case (JCD_TYPE)
         call eval_atmos_coeffs_isothrm(ml, pt_o, bd%V_g_o, &
            bd%As_o, bd%c_1_o)
      end select

      bd%U_o = ml%coeff(I_U, pt_o)
      bd%c_1_o = ml%coeff(I_C_1, pt_o)

      ! Other initializations

      bd%n_i = 1
      bd%n_o = 1

      bd%n_e = 2

      ! Finish

      return

   end function rad_bound_t_

   !****

   subroutine build_i(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_i)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the inner boundary conditions

      select case (this%type_i)
      case (REGULAR_TYPE)
         call this%build_regular_i_(st, B)
      case (ZERO_R_TYPE)
         call this%build_zero_r_i_(st, B)
      case default
         @:ABORT('invalid type_i')
      end select

      ! Apply the variables transformation

      call this%tr_i%trans_cond(B, st)

      ! Finish

      return

   end subroutine build_i

   !****

   subroutine build_regular_i_(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      real(RD) :: omega_c

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_i)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the inner boundary conditions (regular-enforcing)

      associate( &
         omega => st%omega, &
         c_1 => this%c_1_i, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Set up the boundary conditions

         B(1,1) = c_1*alpha_omg*omega_c**2
         B(1,2) = 0._RD

      end associate

      ! Finish

      return

   end subroutine build_regular_i_

   !****

   subroutine build_zero_r_i_(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_i)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the inner boundary conditions (zero radial
      ! displacement)

      ! Set up the boundary conditions

      B(1,1) = 1._RD
      B(1,2) = 0._RD

      ! Finish

      return

   end subroutine build_zero_r_i_

   !****

   subroutine build_o (this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions

      select case (this%type_o)
      case (VACUUM_TYPE)
         call this%build_vacuum_o_(st, B)
      case (ZERO_R_TYPE)
         call this%build_zero_r_o_(st, B)
      case (DZIEM_TYPE)
         call this%build_dziem_o_(st, B)
      case (ISOTHRM_TYPE)
         call this%build_decomp_o_(st, B)
      case (UNNO_TYPE)
         call this%build_decomp_o_(st, B)
      case (JCD_TYPE)
         call this%build_jcd_o_(st, B)
      case default
         @:ABORT('invalid type_o')
      end select

      ! Apply the variables transformation

      call this%tr_o%trans_cond(B, st)

      ! Finish

      return

   end subroutine build_o

   !****

   subroutine build_vacuum_o_(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions (vacuum)

      ! Set up the boundary conditions

      B(1,1) = 1._RD
      B(1,2) = -1._RD

      ! Finish

      return

   end subroutine build_vacuum_o_

   !****

   subroutine build_zero_r_o_(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions (zero radial
      ! displacement)

      ! Set up the boundary conditions

      B(1,1) = 1._RD
      B(1,2) = 0._RD

      ! Finish

      return

   end subroutine build_zero_r_o_

   !****

   subroutine build_dziem_o_(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      real(RD) :: omega_c

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions ([Dzi1971] formulation)

      associate( &
         omega => st%omega, &
         V => this%V_o, &
         c_1 => this%c_1_o, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Set up the boundary conditions

         B(1,1) = 1 - (4._RD + c_1*alpha_omg*omega_c**2)/V
         B(1,2) = -1._RD

      end associate

      ! Finish

      return

   end subroutine build_dziem_o_

   !****

   subroutine build_decomp_o_(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      real(RD) :: omega_c
      real(RD) :: a_11
      real(RD) :: a_12
      real(RD) :: chi

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions, based on a local
      ! eigendecomposition

      associate( &
         omega => st%omega, &
         V_g => this%V_g_o, &
         As => this%As_o, &
         c_1 => this%c_1_o, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Evaluate selected elements of the Jacobian matrix

         a_11 = V_g - 3._RD
         a_12 = -V_g

         ! Evaluate the eigenvalue for the wave we want to keep

         chi = atmos_chi(V_g, As, c_1, alpha_omg, omega_c, 0._RD)

         ! Set up the boundary conditions

         B(1,1) = -(chi - a_11)
         B(1,2) = a_12

      end associate

      ! Finish

      return

   end subroutine build_decomp_o_

   !****

   subroutine build_jcd_o_(this, st, B)

      class(rad_bound_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: B(:,:)

      real(RD) :: omega_c
      real(RD) :: chi
      real(RD) :: b_11
      real(RD) :: b_12

      @:CHECK_BOUNDS(SIZE(B, 1), this%n_o)
      @:CHECK_BOUNDS(SIZE(B, 2), this%n_e)

      ! Evaluate the outer boundary conditions ([Chr2008] formulation)

      ! Calculate coefficients

      associate( &
         omega => st%omega, &
         V_g => this%V_g_o, &
         As => this%As_o, &
         c_1 => this%c_1_o, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         chi = atmos_chi(V_g, As, c_1, alpha_omg, omega_c, 0._RD)

         b_11 = V_g - 3._RD
         b_12 = -V_g

         ! Set up the boundary conditions

         B(1,1) = chi - b_11
         B(1,2) = -b_12

      end associate

      ! Finish

      return

   end subroutine build_jcd_o_

end module rad_bound_m
