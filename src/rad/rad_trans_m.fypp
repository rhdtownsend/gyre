! Module  : rad_trans_m
! Purpose : adiabatic radial variables transformations
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module rad_trans_m

   ! Uses

   use forum_m, only: RD

   use context_m
   use model_m
   use model_util_m
   use mode_par_m
   use osc_par_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Parameter definitions

   integer, parameter :: SET = 0
   integer, parameter :: DZIEM_SET = 1
   integer, parameter :: JCD_SET = 2
   integer, parameter :: MIX_SET = 3
   integer, parameter :: LAGP_SET = 4

   ! Derived-type definitions

   type :: rad_trans_t
      private
      type(context_t), pointer :: cx => null()
      type(point_t)            :: pt
      real(RD)                 :: V_2
      real(RD)                 :: dV_2
      real(RD)                 :: c_1
      real(RD)                 :: dc_1
      real(RD)                 :: x
      integer                  :: set
      integer                  :: n_e
   contains
      private
      procedure, public :: trans_eqns
      procedure, public :: trans_cond
      procedure, public :: trans_vars
      procedure         :: G_
      procedure         :: G_jcd_
      procedure         :: G_lagp_
      procedure         :: H_
      procedure         :: H_jcd_
      procedure         :: H_lagp_
      procedure         :: dH_
      procedure         :: dH_jcd_
      procedure         :: dH_lagp_
   end type rad_trans_t

   ! Interfaces

   interface rad_trans_t
      module procedure rad_trans_t_
   end interface rad_trans_t

   ! Access specifiers

   public :: rad_trans_t

   ! Default access

   private

contains

   function rad_trans_t_(cx, pt, md_p, os_p) result(tr)

      type(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)            :: pt
      type(mode_par_t), intent(in)         :: md_p
      type(osc_par_t), intent(in)          :: os_p
      type(rad_trans_t)                    :: tr

      class(model_t), pointer :: ml

      ! Construct the rad_trans_t

      select case (os_p%variables_set)
      case ('GYRE')
         tr%set = SET
      case ('DZIEM')
         tr%set = DZIEM_SET
      case ('JCD')
         tr%set = JCD_SET
      case ('MIX')
         tr%set = MIX_SET
      case ('LAGP')
         tr%set = LAGP_SET
      case default
         @:ABORT('invalid variables_set')
      end select

      tr%cx => cx

      tr%pt = pt

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [I_V_2,I_U,I_C_1])

      if (ml%is_vacuum(pt)) then
         @:ASSERT(tr%set /= LAGP_SET, 'LAGP invalid at vacuum point')
         tr%V_2 = HUGE(0._RD)
         tr%dV_2 = HUGE(0._RD)
      else
         tr%V_2 = ml%coeff(I_V_2, pt)
         tr%dV_2 = ml%dcoeff(I_V_2, pt)
      endif

      tr%c_1 = ml%coeff(I_C_1, pt)
      tr%dc_1 = ml%dcoeff(I_C_1, pt)

      ! Other initializations

      tr%x = pt%x

      tr%n_e = 2

      ! Finish

      return

   end function rad_trans_t_

   !****

   subroutine trans_eqns(this, A, st, from)

      class(rad_trans_t), intent(in) :: this
      real(RD), intent(inout)        :: A(:,:)
      class(state_rt), intent(in)    :: st
      logical, intent(in), optional  :: from

      logical               :: from_
      real(RD), allocatable :: G(:,:)
      real(RD), allocatable :: H(:,:)
      real(RD), allocatable :: dH(:,:)

      @:CHECK_BOUNDS(SIZE(A, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(A, 2), this%n_e)

      if (PRESENT(from)) then
         from_ = from
      else
         from_ = .TRUE.
      endif

      ! Transform equations to/from GYRE's canonical form

      if (from_) then

         ! Convert from

         if (this%set /= SET .AND. &
            this%set /= DZIEM_SET .AND. &
            this%set /= MIX_SET) then
            G = this%G_(st)
            H = this%H_(st)
            dH = this%dH_(st)
            A = MATMUL(G, MATMUL(A, H) - dH)
         endif

      else

         ! Convert to

         @:ABORT('not currently supported')

      end if

      ! Finish

      return

   end subroutine trans_eqns

   !****

   subroutine trans_cond(this, C, st, from)

      class(rad_trans_t), intent(in) :: this
      real(RD), intent(inout)        :: C(:,:)
      class(state_rt), intent(in)    :: st
      logical, intent(in), optional  :: from

      logical               :: from_
      real(RD), allocatable :: G(:,:)
      real(RD), allocatable :: H(:,:)

      @:CHECK_BOUNDS(SIZE(C, 2), this%n_e)

      if (PRESENT(from)) then
         from_ = from
      else
         from_ = .TRUE.
      endif

      ! Transform boundary/match conditions to/from GYRE's canonical form

      if (from_) then

         ! Convert from

         if (this%set /= SET .AND. &
            this%set /= DZIEM_SET .AND. &
            this%set /= MIX_SET) then
            H = this%H_(st)
            C = MATMUL(C, H)
         endif

      else

         ! Convert to

         if (this%set /= SET .AND. &
            this%set /= DZIEM_SET .AND. &
            this%set /= MIX_SET) then
            G = this%G_(st)
            C = MATMUL(C, G)
         endif

      end if

      ! Finish

      return

   end subroutine trans_cond

   !****

   subroutine trans_vars(this, y, st, from)

      class(rad_trans_t), intent(in) :: this
      real(RD), intent(inout)        :: y(:)
      class(state_rt), intent(in)    :: st
      logical, intent(in), optional  :: from

      logical               :: from_
      real(RD), allocatable :: G(:,:)
      real(RD), allocatable :: H(:,:)

      @:CHECK_BOUNDS(SIZE(y), this%n_e)

      if (PRESENT(from)) then
         from_ = from
      else
         from_ = .TRUE.
      endif

      ! Convert variables to/from GYRE's canonical form

      if (from_) then

         ! Convert from

         if (this%set /= SET .AND. &
            this%set /= DZIEM_SET .AND. &
            this%set /= MIX_SET) then
            G = this%G_(st)
            y = MATMUL(G, y)
         endif

      else

         ! Convert to

         if (this%set /= SET .AND. &
            this%set /= DZIEM_SET .AND. &
            this%set /= MIX_SET) then
            H = this%H_(st)
            y = MATMUL(H, y)
         endif

      end if

      ! Finish

      return

   end subroutine trans_vars

   !****

   function G_(this, st) result(G)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: G(this%n_e,this%n_e)

      ! Evaluate the transformation matrix to convert variables from
      ! GYRE's canonical form

      select case (this%set)
      case (JCD_SET)
         G = this%G_jcd_(st)
      case (LAGP_SET)
         G = this%G_lagp_(st)
      case default
         @:ABORT('invalid set')
      end select

      ! Finish

      return

   end function G_

   !****

   function G_jcd_(this, st) result(G)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: G(this%n_e,this%n_e)

      real(RD) :: omega_c

      ! Evaluate the transformation matrix to convert JCD variables
      ! from GYRE's canonical form

      associate( &
         omega => st%omega, &
         c_1 => this%c_1)

         omega_c = omega

         ! Set up the matrix

         G(1,1) = 1._RD
         G(1,2) = 0._RD

         G(2,1) = 0._RD
         G(2,2) = 1._RD/(c_1*omega_c**2)

      end associate

      ! Finish

      return

   end function G_jcd_

   !****

   function G_lagp_(this, st) result(G)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: G(this%n_e,this%n_e)

      ! Evaluate the transformation matrix to convert LAGP variables
      ! from GYRE's canonical form

      associate( &
         V_2 => this%V_2)

         ! Set up the matrix

         G(1,1) = 1._RD
         G(1,2) = 0._RD

         G(2,1) = -V_2
         G(2,2) = V_2

      end associate

      ! Finish

      return

   end function G_lagp_

   !****

   function H_(this, st) result(H)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: H(this%n_e,this%n_e)

      ! Evaluate the transformation matrix to convert variables to
      ! canonical form

      select case (this%set)
      case (JCD_SET)
         H = this%H_jcd_(st)
      case (LAGP_SET)
         H = this%H_lagp_(st)
      case default
         @:ABORT('invalid vars')
      end select

      ! Finish

      return

   end function H_

   !****

   function H_jcd_(this, st) result(H)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: H(this%n_e,this%n_e)

      real(RD) :: omega_c

      ! Evaluate the transformation matrix to convert JCD variables
      ! to GYRE's canonical form

      associate( &
         omega => st%omega, &
         c_1 => this%c_1)

         omega_c = omega

         ! Set up the matrix

         H(1,1) = 1._RD
         H(1,2) = 0._RD

         H(2,1) = 0._RD
         H(2,2) = c_1*omega_c**2

      end associate

      ! Finish

      return

   end function H_jcd_

   !****

   function H_lagp_(this, st) result(H)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: H(this%n_e,this%n_e)

      ! Evaluate the transformation matrix to convert LAGP variables
      ! to GYRE's canonical form

      associate( &
         V_2 => this%V_2)

         ! Set up the matrix

         H(1,1) = 1._RD
         H(1,2) = 0._RD

         H(2,1) = 1._RD
         H(2,2) = 1._RD/V_2

      end associate

      ! Finish

      return

   end function H_lagp_

   !****

   function dH_(this, st) result(dH)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: dH(this%n_e,this%n_e)

      ! Evaluate the derivative dH/dx of the transformation matrix H

      select case (this%set)
      case (JCD_SET)
         dH = this%dH_jcd_(st)
      case (LAGP_SET)
         dH = this%dH_lagp_(st)
      case default
         @:ABORT('invalid set')
      end select

      ! Finish

      return

   end function dH_

   !****

   function dH_jcd_(this, st) result(dH)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: dH(this%n_e,this%n_e)

      real(RD) :: omega_c
      real(RD) :: invx

      ! Evaluate the derivative dH/dx of the JCD-variables
      ! transformation matrix H

      associate( &
         omega => st%omega, &
         c_1 => this%c_1, &
         dc_1 => this%dc_1, &
         x => this%x)

         omega_c = omega

         ! Set up the matrix

         invx = 1._RD/x

         dH(1,1) = invx*0._RD
         dH(1,2) = invx*0._RD

         dH(2,1) = invx*0._RD
         dH(2,2) = invx*c_1*dc_1*omega_c**2

      end associate

      ! Finish

      return

   end function dH_jcd_

   !****

   function dH_lagp_(this, st) result(dH)

      class(rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD)                       :: dH(this%n_e,this%n_e)

      real(RD) :: invx
      
      ! Evaluate the derivative dH/dx of the LAGP-variables
      ! transformation matrix H

      associate( &
         V_2 => this%V_2, &
         dV_2 => this%dV_2, &
         x => this%x)

         ! Set up the matrix

         invx = 1._RD/x

         dH(1,1) = invx*0._RD
         dH(1,2) = invx*0._RD

         dH(2,1) = invx*0._RD
         dH(2,2) = invx*(-dV_2/V_2)

      end associate

      ! Finish

      return

   end function dH_lagp_

end module rad_trans_m
