! Module  : rad_trans_m
! Purpose : radial adiabatic variables/equations transformations
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

#:set P = os.path.basename(_FILE_).split('_')[0]

module ${P}$_rad_trans_m

   ! Uses

   use forum_m, only: RD

   use context_m
   use model_m
   use model_util_m
   use point_m
   use state_m
   use trans_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(trans_rt) :: ${P}$_rad_trans_t
      private
      type(context_t), pointer :: cx => null()
      real(RD)                 :: V_2
      real(RD)                 :: U
      real(RD)                 :: c_1
      real(RD)                 :: x
   contains
      private
      procedure, public :: transform
   end type ${P}$_rad_trans_t

   ! Interfaces

   interface ${P}$_rad_trans_t
      module procedure ${P}$_rad_trans_t_
   end interface ${P}$_rad_trans_t

   ! Access specifiers

   public :: ${P}$_rad_trans_t

   ! Default access

   private

contains

   function ${P}$_rad_trans_t_(cx, pt) result(tr)

      type(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)            :: pt
      type(${P}$_rad_trans_t)              :: tr

      class(model_t), pointer :: ml

      ! Construct the ${P}$_rad_trans type

      tr%cx => cx

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [I_V_2,I_U,I_C_1,I_OMEGA_ROT])

      if (ml%is_vacuum(pt)) then
         tr%V_2 = HUGE(0._RD)
      else
         tr%V_2 = ml%coeff(I_V_2, pt)
      endif

      tr%U = ml%coeff(I_U, pt)
      tr%c_1 = ml%coeff(I_C_1, pt)

      ! Other initializations

      tr%x = pt%x

      tr%n_e = 2

      ! Finish

      return

   end function ${P}$_rad_trans_t_

   !****

   subroutine transform(this, st, y)

      class(${P}$_rad_trans_t), intent(in) :: this
      class(state_rt), intent(in)          :: st
      real(RD), intent(inout)              :: y(:)

      real(RD) :: omega_c
      real(RD) :: R(this%n_e,this%n_e)

      @:CHECK_BOUNDS(SIZE(y), this%n_e)

      ! Transform the variables to canonical form

      associate( &
         omega => st%omega, &
         V_2 => this%V_2, &
         U => this%U, &
         c_1 => this%c_1, &
         alpha_omg => this%cx%alpha_omg)

         omega_c = omega

         ! Set up the transformation matrix

         #:include 'R.inc'

         ! Apply it

         y = MATMUL(R, y)

      end associate

      ! Finish

      return

   end subroutine transform

end module ${P}$_rad_trans_m
