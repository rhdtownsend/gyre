! Module  : rad_bvp_m
! Purpose : radial adiabatic bounary value problem solver
!
! Copyright 2013-2022 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module rad_bvp_m

   ! Uses

   use forum_m, only: RD

   use bvp_m
   use context_m
   use ext_m
   use grid_m
   use integ_m
   use model_m
   use mode_par_m
   use num_par_m
   use osc_par_m
   use point_m
   use qad_eval_m
   use rad_bound_m
   use rad_diff_m
   use rad_trans_m
   use state_m
   use wave_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, extends(bvp_rt) :: rad_bvp_t
      private
      type(context_t), pointer :: cx => null()
      type(grid_t)             :: gr
      type(rad_trans_t)        :: tr
      type(qad_eval_t)         :: qe
      type(mode_par_t)         :: md_p
      type(num_par_t)          :: nm_p
      type(osc_par_t)          :: os_p
   end type rad_bvp_t

   ! Interfaces

   interface rad_bvp_t
      module procedure rad_bvp_t_
   end interface rad_bvp_t

   interface wave_t
      module procedure wave_t_hom_
      module procedure wave_t_inhom_
   end interface wave_t

   ! Access specifiers

   public :: rad_bvp_t
   public :: wave_t

   ! Default access

   private

contains

   function rad_bvp_t_(cx, gr, md_p, nm_p, os_p) result(bp)

      type(context_t), pointer, intent(in) :: cx
      type(grid_t), intent(in)             :: gr
      type(mode_par_t), intent(in)         :: md_p
      type(num_par_t), intent(in)          :: nm_p
      type(osc_par_t), intent(in)          :: os_p
      type(rad_bvp_t)                      :: bp

      type(point_t)                 :: pt_i
      type(point_t)                 :: pt_o
      type(rad_bound_t)             :: bd
      integer                       :: j
      type(rad_diff_t), allocatable :: df(:)
      type(osc_par_t)               :: qad_os_p

      ! Construct the rad_bvp_t

      if (os_p%alpha_grv /= 1._RD) then
         @:WARN('alpha_grv is ignored in 2nd-order radial equations')
      endif

      pt_i = gr%pt_i()
      pt_o = gr%pt_o()

      ! Initialize the boundary conditions

      bd = rad_bound_t(cx, md_p, os_p)

      ! Initialize the difference equations

      allocate(df(gr%n-1))

      !$OMP PARALLEL DO
      do j = 1, gr%n-1
         df(j) = rad_diff_t(cx, gr%pt(j), gr%pt(j+1), md_p, nm_p, os_p)
      end do

      ! Initialize the bvp_t

      bp%bvp_rt = bvp_rt(bd, df, nm_p)

      ! Other initializations

      bp%cx => cx
      bp%gr = gr

      bp%tr = rad_trans_t(cx, md_p, os_p)
      call bp%tr%stencil(gr%pt)

      if (os_p%quasiad_eigfuncs) then
         qad_os_p = os_p
         qad_os_p%variables_set = 'GYRE'
         bp%qe = qad_eval_t(cx, gr, md_p, qad_os_p)
      endif

      bp%md_p = md_p
      bp%nm_p = nm_p
      bp%os_p = os_p

      ! Finish

      return

   end function rad_bvp_t_

   !****

   function wave_t_hom_(bp, st, id) result(wv)

      class(rad_bvp_t), intent(inout) :: bp
      type(state_rt), intent(in)      :: st
      integer, intent(in)             :: id
      type(wave_t)                    :: wv

      real(RD) :: y(2,bp%n)
      integer  :: j

      ! Calculate the solution vector

      call bp%build(st)
      call bp%factor()

      y = bp%soln_vec_hom()

      ! Convert to canonical form

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr%trans_vars(y(:,j), j, st, from=.FALSE.)
      end do

      ! Construct the wave_t

      wv = wave_t_y_(bp, st, y, id)

      ! Finish

      return

   end function wave_t_hom_

   !****

   function wave_t_inhom_(bp, st, z_i, z_o, id) result(wv)

      class(rad_bvp_t), intent(inout) :: bp
      type(state_rt), intent(in)      :: st
      real(RD), intent(in)            :: z_i(:)
      real(RD), intent(in)            :: z_o(:)
      integer, intent(in)             :: id
      type(wave_t)                    :: wv

      real(RD) :: y(2,bp%n)
      integer  :: j

      @:CHECK_BOUNDS(SIZE(z_i), bp%n_i)
      @:CHECK_BOUNDS(SIZE(z_o), bp%n_o)

      ! Calculate the solution vector

      call bp%build(st)
      call bp%factor()

      y = bp%soln_vec_inhom(z_i, z_o)

      ! Convert to canonical form

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr%trans_vars(y(:,j), j, st, from=.FALSE.)
      end do

      ! Construct the wave_t

      wv = wave_t_y_(bp, st, y, id)

      ! Finish

      return

   end function wave_t_inhom_

   !****

   function wave_t_y_(bp, st, y, id) result(wv)

      class(rad_bvp_t), intent(inout) :: bp
      type(state_rt), intent(in)      :: st
      real(RD), intent(in)            :: y(:,:)
      integer, intent(in)             :: id
      type(wave_t)                    :: wv

      class(model_t), pointer :: ml
      integer                 :: j
      real(RD)                :: U
      real(RD)                :: c_1(bp%n)
      real(RD)                :: y_4(bp%n)
      real(RD)                :: deul_phi(bp%n)
      integer                 :: s
      integer                 :: j_i
      integer                 :: j_o
      real(RD)                :: eul_phi(bp%n)
      real(RD)                :: y_3(bp%n)
      real(RD)                :: y_g(4,bp%n)
      complex(RD)             :: y_c(6,bp%n)
      type(state_ct)          :: st_c
      type(ext_ct)            :: discrim

      ! Set up gravitational eigenfunctions

      ml => bp%cx%model()

      !$OMP PARALLEL DO PRIVATE (U)
      do j = 1, bp%n

         associate ( pt => bp%gr%pt(j) )

            U = ml%coeff(I_U, pt)
            c_1(j) = ml%coeff(I_C_1, pt)

            ! Evaluate y_4

            y_4(j) = -U*y(1,j)

            ! Evaluate the Eulerian potential gradient (gravity) perturbation

            if (pt%x /= 0._RD) then
               deul_phi(j) = y_4(j)/(c_1(j)*pt%x)
            else
               deul_phi(j) = 0._RD
            end if

         end associate

      end do

      ! Evaluate the Eulerian potential perturbation,
      ! segment-by-segment, by integrating an interpolant fit to the
      ! gravity perturbation

      seg_loop : do s = bp%gr%s_i(), bp%gr%s_o()

         j_i = bp%gr%j_s_i(s)
         j_o = bp%gr%j_s_o(s)

         eul_phi(j_i:j_o) = integral(bp%gr%pt(j_i:j_o)%x, deul_phi(j_i:j_o))

         if (s > bp%gr%s_i()) then
            eul_phi(j_i:j_o) = eul_phi(j_i:j_o) + eul_phi(j_i-1)
         end if

      end do seg_loop

      ! Adjust the potential perturbation so that it satisfies the
      ! surface boundary condition

      eul_phi = eul_phi - eul_phi(bp%n) - y_4(bp%n)

      ! Set up y_3 based on it

      y_3 = c_1*eul_phi

      ! Store eigenfunctions into y_g

      y_g(1:2,:) = y

      y_g(3,:) = y_3
      y_g(4,:) = y_4

      ! Set up complex eigenfunctions

      st_c = state_ct(CMPLX(st%omega, KIND=RD), st%omega)

      if (bp%os_p%quasiad_eigfuncs) then

         y_c = bp%qe%y_qad(st_c, y_g)

      else

         y_c(1:4,:) = y_g
         y_c(5:6,:) = 0._RD

      endif

      ! Construct the wave_t

      discrim = ext_ct(bp%det())

      wv = wave_t(st_c, y_c, discrim, bp%cx, bp%gr, bp%md_p, bp%nm_p, bp%os_p, id)

      ! Finish

      return

   end function wave_t_y_

end module rad_bvp_m
