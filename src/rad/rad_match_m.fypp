! Module  : rad_match_m
! Purpose : adiabatic radial match conditions
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module rad_match_m

   ! Uses

   use forum_m, only: RD

   use context_m
   use diff_m
   use ext_m
   use model_m
   use model_util_m
   use mode_par_m
   use osc_par_m
   use point_m
   use rad_trans_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(diff_rt) :: rad_match_t
      private
      type(context_t), pointer :: cx => null()
      type(rad_trans_t)        :: tr_a
      type(rad_trans_t)        :: tr_b
      real(RD)                 :: U_a
      real(RD)                 :: U_b
   contains
      private
      procedure, public :: build
   end type rad_match_t

   ! Interfaces

   interface rad_match_t
      module procedure rad_match_t_
   end interface rad_match_t

   ! Access specifiers

   private
   public :: rad_match_t

contains

   function rad_match_t_(cx, pt_a, pt_b, md_p, os_p) result(mt)

      type(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)            :: pt_a
      type(point_t), intent(in)            :: pt_b
      type(mode_par_t), intent(in)         :: md_p
      type(osc_par_t), intent(in)          :: os_p
      type(rad_match_t)                    :: mt

      class(model_t), pointer :: ml

      @:ASSERT_DEBUG(pt_a%s+1 == pt_b%s, 'mismatched segments')
      @:ASSERT_DEBUG(pt_a%x == pt_b%x, 'mismatched abscissae')

      ! Construct the rad_match_t

      mt%cx => cx

      mt%tr_a = rad_trans_t(cx, pt_a, md_p, os_p)
      mt%tr_b = rad_trans_t(cx, pt_b, md_p, os_p)

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [I_U])

      mt%U_a = ml%coeff(I_U, pt_a)
      mt%U_b = ml%coeff(I_U, pt_b)

      ! Other initializations

      mt%n_e = 2

      ! Finish

      return

   end function rad_match_t_

   !****

   subroutine build(this, st, E_l, E_r, scl)

      class(rad_match_t), intent(in) :: this
      class(state_rt), intent(in)    :: st
      real(RD), intent(out)          :: E_l(:,:)
      real(RD), intent(out)          :: E_r(:,:)
      type(ext_rt), intent(out)      :: scl

      @:CHECK_BOUNDS(SIZE(E_l, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(E_l, 2), this%n_e)

      @:CHECK_BOUNDS(SIZE(E_r, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(E_r, 2), this%n_e)

      ! Build the difference equations

      ! Calculate coefficients

      associate( &
         U_l => this%U_a, &
         U_r => this%U_b)

         ! Evaluate the match conditions (y_1 continuous, y_2 not)

         E_l(1,1) = -1._RD
         E_l(1,2) = 0._RD

         E_l(2,1) = U_l
         E_l(2,2) = -U_l

         !

         E_r(1,1) = 1._RD
         E_r(1,2) = 0._RD

         E_r(2,1) = -U_r
         E_r(2,2) = U_r

         scl = ONE_RT

      end associate

      ! Apply the variables transformation

      call this%tr_a%trans_cond(E_l, st)
      call this%tr_b%trans_cond(E_r, st)

      ! Finish

      return

   end subroutine build

end module rad_match_m
