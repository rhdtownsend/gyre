! Module  : rad_eqns_m
! Purpose : radial adiabatic differential equations
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module rad_eqns_m

   ! Uses

   use forum_m, only: RD

   use context_m
   use eqns_m
   use model_m
   use model_util_m
   use mode_par_m
   use osc_par_m
   use point_m
   use rad_trans_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends (eqns_rt) :: rad_eqns_t
      private
      type(context_t), pointer :: cx => null()
      type(rad_trans_t)        :: tr
      real(RD)                 :: V_g
      real(RD)                 :: As
      real(RD)                 :: U
      real(RD)                 :: c_1
      real(RD)                 :: x
      real(RD)                 :: alpha_omg
   contains
      private
      procedure, public :: eval_A
   end type rad_eqns_t

   ! Interfaces

   interface rad_eqns_t
      module procedure rad_eqns_t_
   end interface rad_eqns_t

   ! Access specifiers

   public :: rad_eqns_t

   ! Default access

   private

contains

   function rad_eqns_t_(cx, pt, md_p, os_p) result(eq)

      class(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)             :: pt
      type(mode_par_t), intent(in)          :: md_p
      type(osc_par_t), intent(in)           :: os_p
      type(rad_eqns_t)                      :: eq

      class(model_t), pointer :: ml

      ! Construct the rad_eqns_t

      eq%cx => cx

      eq%tr = rad_trans_t(cx, pt, md_p, os_p)

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [I_V_2,I_AS,I_U,I_C_1,I_GAMMA_1])

      @:ASSERT(.NOT. ml%is_vacuum(pt), 'vacuum point')

      eq%V_g = ml%coeff(I_V_2, pt)*pt%x**2/ml%coeff(I_GAMMA_1, pt)
      eq%As = ml%coeff(I_AS, pt)
      eq%U = ml%coeff(I_U, pt)
      eq%c_1 = ml%coeff(I_C_1, pt)

      ! Other initializations

      eq%x = pt%x

      select case (os_p%time_factor)
      case ('OSC')
         eq%alpha_omg = 1._RD
      case ('EXP')
         eq%alpha_omg = -1._RD
      case default
         @:ABORT('invalid time_factor')
      end select

      eq%n_e = 2

      ! Finish

      return

   end function rad_eqns_t_

   !****

   subroutine eval_A(this, st, A)

      class(rad_eqns_t), intent(in) :: this
      class(state_rt), intent(in)   :: st
      real(RD), intent(out)         :: A(:,:)

      real(RD) :: omega_c
      real(RD) :: invx

      @:CHECK_BOUNDS(SIZE(A, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(A, 2), this%n_e)

      ! Evaluate the RHS matrix

      associate ( &
         omega => st%omega, &
         V_g => this%V_g, &
         As => this%As, &
         U => this%U, &
         c_1 => this%c_1, &
         x => this%x, &
         alpha_omg => this%alpha_omg)

         omega_c = omega

         ! Set up the matrix

         invx = 1._RD/x

         A(1,1) = invx*(V_g - 1._RD)
         A(1,2) = invx*(-V_g)

         A(2,1) = invx*(c_1*alpha_omg*omega_c**2 + U - As)
         A(2,2) = invx*(As - U + 3._RD)

         ! Apply the variables transformation

         call this%tr%trans_eqns(A, st)

      end associate

      ! Finish

      return

   end subroutine eval_A

end module rad_eqns_m
