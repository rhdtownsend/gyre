! Module  : ad_bvp_m
! Purpose : adiabatic bounary value problem solver
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module ad_bvp_m

   ! Uses

   use forum_m, only: RD

   use ad_bound_m
   use ad_diff_m
   use ad_eqns_m
   use ad_trans_m
   use bvp_m
   use context_m
   use ext_m
   use grid_m
   use model_m
   use mode_par_m
   use num_par_m
   use osc_par_m
   use point_m
   use qad_eval_m
   use state_m
   use wave_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(bvp_rt) :: ad_bvp_t
      private
      type(context_t), pointer :: cx => null()
      type(ad_trans_t)         :: tr
      type(grid_t)             :: gr
      type(qad_eval_t)         :: qe
      type(mode_par_t)         :: md_p
      type(num_par_t)          :: nm_p
      type(osc_par_t)          :: os_p
   end type ad_bvp_t

   ! Interfaces

   interface ad_bvp_t
      module procedure ad_bvp_t_
   end interface ad_bvp_t

   interface wave_t
      module procedure wave_t_hom_
      module procedure wave_t_inhom_
   end interface wave_t

   ! Access specifiers

   public :: ad_bvp_t
   public :: wave_t

   ! Default access

   private

contains

   function ad_bvp_t_(cx, gr, md_p, nm_p, os_p) result(bp)

      type(context_t), pointer, intent(in) :: cx
      type(grid_t), intent(in)             :: gr
      type(mode_par_t), intent(in)         :: md_p
      type(num_par_t), intent(in)          :: nm_p
      type(osc_par_t), intent(in)          :: os_p
      type(ad_bvp_t)                       :: bp

      type(ad_bound_t)             :: bd
      integer                      :: j
      type(ad_diff_t), allocatable :: df(:)
      type(osc_par_t)              :: qad_os_p

      ! Construct the ad_bvp type

      ! Initialize the boundary conditions

      bd = ad_bound_t(cx, md_p, os_p)

      ! Initialize the difference equations

      allocate(df(gr%n-1))

      !$OMP PARALLEL DO
      do j = 1, gr%n-1
         df(j) = ad_diff_t(cx, gr%pt(j), gr%pt(j+1), md_p, nm_p, os_p)
      end do

      ! Initialize the bvp_t

      bp%bvp_rt = bvp_rt(bd, df, nm_p)

      ! Other initializations

      bp%cx => cx
      bp%gr = gr

      bp%tr = ad_trans_t(cx, md_p, os_p)
      call bp%tr%stencil(gr%pt)

      if (os_p%quasiad_eigfuncs) then
         qad_os_p = os_p
         qad_os_p%variables_set = 'GYRE'
         bp%qe = qad_eval_t(cx, gr, md_p, qad_os_p)
      endif

      bp%md_p = md_p
      bp%nm_p = nm_p
      bp%os_p = os_p

      ! Finish

      return

   end function ad_bvp_t_

   !****

   function wave_t_hom_(bp, st, id, chi, n_iter) result(wv)

      class(ad_bvp_t), intent(inout) :: bp
      type(state_rt), intent(in)     :: st
      integer, intent(in)            :: id
      real(RD), intent(in)           :: chi
      integer, intent(in)            :: n_iter
      type(wave_t)                   :: wv

      real(RD) :: y(4,bp%n)
      integer  :: j

      ! Calculate the homogeneous solution vector

      call bp%solve_hom(st, y)

      ! Convert to canonical form

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr%trans_vars(y(:,j), j, st, from=.FALSE.)
      end do

      ! Construct the wave type

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_hom_

   !****

   function wave_t_inhom_(bp, st, z_i, z_o, id, chi, n_iter) result(wv)

      class(ad_bvp_t), intent(inout) :: bp
      type(state_rt), intent(in)     :: st
      real(RD), intent(in)           :: z_i(:)
      real(RD), intent(in)           :: z_o(:)
      integer, intent(in)            :: id
      real(RD), intent(in)           :: chi
      integer, intent(in)            :: n_iter
      type(wave_t)                   :: wv

      real(RD) :: y(4,bp%n)
      integer  :: j

      @:CHECK_BOUNDS(SIZE(z_i), bp%n_i)
      @:CHECK_BOUNDS(SIZE(z_o), bp%n_o)

      ! Calculate the inhomogeneous solution vector

      call bp%solve_inhom(st, z_i, z_o, y)

      ! Convert to canonical form

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr%trans_vars(y(:,j), j, st, from=.FALSE.)
      end do

      ! Construct the wave type

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_inhom_

   !****

   function wave_t_y_(bp, st, y, id, chi, n_iter) result(wv)

      class(ad_bvp_t), intent(inout) :: bp
      type(state_rt), intent(in)     :: st
      real(RD), intent(in)           :: y(:,:)
      integer, intent(in)            :: id
      real(RD), intent(in)           :: chi
      integer, intent(in)            :: n_iter
      type(wave_t)                   :: wv

      type(state_ct) :: st_c
      complex(RD)    :: y_c(6,bp%n)

      @:CHECK_BOUNDS(SIZE(y, 1), bp%n_e)
      @:CHECK_BOUNDS(SIZE(y, 2), bp%n)

      ! Set up complex eigenfunctions

      st_c = state_ct(CMPLX(st%omega, KIND=RD), st%omega)

      if (bp%os_p%quasiad_eigfuncs) then

         y_c = bp%qe%y_qad(st_c, y)

      else

         y_c(1:4,:) = y
         y_c(5:6,:) = 0._RD

      endif

      ! Construct the wave type

      wv = wave_t(st_c, y_c, bp%cx, bp%gr, bp%md_p, bp%nm_p, bp%os_p, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_y_

end module ad_bvp_m
