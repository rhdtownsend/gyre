! Module  : ad_bvp_m
! Purpose : adiabatic bounary value problem solver
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module ad_bvp_m

   ! Uses

   use forum_m, only: RD

   use bound_m
   use bvp_m
   use context_m
   use diff_element_m
   use diff_factory_m
   use eqns_m
   use ext_m
   use grid_m
   use model_m
   use mode_par_m
   use num_par_m
   use osc_par_m
   use point_m
   use qad_eval_m
   use state_m
   use sysmtx_m
   use trans_m
   use wave_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(bvp_rt) :: ad_bvp_t
      private
      type(context_t), pointer     :: cx => null()
      class(trans_rt), allocatable :: tr(:)
      type(grid_t)                 :: gr
      type(qad_eval_t)             :: qe
      type(mode_par_t)             :: md_p
      type(num_par_t)              :: nm_p
      type(osc_par_t)              :: os_p
   end type ad_bvp_t

   ! Interfaces

   interface ad_bvp_t
      module procedure ad_bvp_t_
   end interface ad_bvp_t

   interface wave_t
      module procedure wave_t_hom_
      module procedure wave_t_inhom_
   end interface wave_t

   ! Access specifiers

   public :: ad_bvp_t
   public :: wave_t

   ! Default access

   private

contains

   function ad_bvp_t_(cx, gr, md_p, nm_p, os_p) result(bp)

      use gyre_ad_bound_m
      use dziem_ad_bound_m
      use jcd_ad_bound_m
      use rjcd_ad_bound_m
      use mix_ad_bound_m
      use lagp_ad_bound_m

      use gyre_ad_trans_m
      use dziem_ad_trans_m
      use jcd_ad_trans_m
      use rjcd_ad_trans_m
      use mix_ad_trans_m
      use lagp_ad_trans_m

      type(context_t), pointer, intent(in) :: cx
      type(grid_t), intent(in)             :: gr
      type(mode_par_t), intent(in)         :: md_p
      type(num_par_t), intent(in)          :: nm_p
      type(osc_par_t), intent(in)          :: os_p
      type(ad_bvp_t)                       :: bp

      class(bound_rt), allocatable       :: bd
      integer                            :: j
      type(diff_element_rt), allocatable :: de(:)
      type(osc_par_t)                    :: qad_os_p

      ! Construct the ad_bvp type

      ! Initialize the boundary conditions

      select case(os_p%variables_set)
      case('GYRE')
         bd = gyre_ad_bound_t(cx, os_p)
      case('DZIEM')
         bd = dziem_ad_bound_t(cx, os_p)
      case('JCD')
         if (md_p%l == 0) then
            bd = rjcd_ad_bound_t(cx, os_p)
         else
            bd = jcd_ad_bound_t(cx, os_p)
         end if
      case('MIX')
         bd = mix_ad_bound_t(cx, os_p)
      case('LAGP')
         bd = lagp_ad_bound_t(cx, os_p)
      case default
         @:ABORT('invalid variables_set')
      end select

      ! Initialize the difference equations

      allocate(de(gr%n-1))

      !$OMP PARALLEL DO
      do j = 1, gr%n-1
         allocate(de(j)%df, SOURCE=diff_rt(eqns_factory_, gr%pt(j), gr%pt(j+1), nm_p))
      end do

      ! Initialize the bvp_t

      bp%bvp_rt = bvp_rt(bd, de, nm_p)

      ! Other initializations

      bp%cx => cx
      bp%gr = gr

      select case(os_p%variables_set)
      case('GYRE')
         allocate(gyre_ad_trans_t::bp%tr(gr%n))
      case('DZIEM')
         allocate(dziem_ad_trans_t::bp%tr(gr%n))
      case('JCD')
         if (md_p%l == 0) then
            allocate(rjcd_ad_trans_t::bp%tr(gr%n))
         else
            allocate(jcd_ad_trans_t::bp%tr(gr%n))
         end if
      case('MIX')
         allocate(mix_ad_trans_t::bp%tr(gr%n))
      case('LAGP')
         allocate(lagp_ad_trans_t::bp%tr(gr%n))
      case default
         @:ABORT('invalid variables_set')
      end select

      !$OMP PARALLEL DO
      do j = 1, gr%n

         select type(tr => bp%tr(j))
         type is(gyre_ad_trans_t)
            tr = gyre_ad_trans_t(cx, gr%pt(j))
         type is(dziem_ad_trans_t)
            tr = dziem_ad_trans_t(cx, gr%pt(j))
         type is(jcd_ad_trans_t)
            tr = jcd_ad_trans_t(cx, gr%pt(j))
         type is(rjcd_ad_trans_t)
            tr = rjcd_ad_trans_t(cx, gr%pt(j))
         type is(mix_ad_trans_t)
            tr = mix_ad_trans_t(cx, gr%pt(j))
         type is(lagp_ad_trans_t)
            tr = lagp_ad_trans_t(cx, gr%pt(j))
         class default
            @:ABORT('invalid type')
         end select

      end do

      if (os_p%quasiad_eigfuncs) then
         qad_os_p = os_p
         qad_os_p%variables_set = 'GYRE'
         bp%qe = qad_eval_t(cx, gr, md_p, qad_os_p)
      endif

      bp%md_p = md_p
      bp%nm_p = nm_p
      bp%os_p = os_p

      ! Finish

      return

   contains

      subroutine eqns_factory_(pt, eq)

         use gyre_ad_eqns_m
         use dziem_ad_eqns_m
         use jcd_ad_eqns_m
         use rjcd_ad_eqns_m
         use mix_ad_eqns_m
         use lagp_ad_eqns_m

         type(point_t), intent(in)                :: pt(:)
         class(eqns_rt), allocatable, intent(out) :: eq(:)

         integer :: n
         integer :: i

         n = SIZE(pt)

         select case(os_p%variables_set)
         case('GYRE')
            allocate(gyre_ad_eqns_t::eq(n))
         case('DZIEM')
            allocate(dziem_ad_eqns_t::eq(n))
         case('JCD')
            if (md_p%l == 0) then
            allocate(rjcd_ad_eqns_t::eq(n))
            else
               allocate(jcd_ad_eqns_t::eq(n))
            end if
         case('MIX')
            allocate(mix_ad_eqns_t::eq(n))
         case('LAGP')
            allocate(lagp_ad_eqns_t::eq(n))
         case default
            @:ABORT('invalid variables_set')
         end select

         do i = 1, n

            select type(eq)
            type is(gyre_ad_eqns_t)
               eq(i) = gyre_ad_eqns_t(cx, pt(i))
            type is(dziem_ad_eqns_t)
               eq(i) = dziem_ad_eqns_t(cx, pt(i))
            type is(jcd_ad_eqns_t)
               eq(i) = jcd_ad_eqns_t(cx, pt(i))
            type is(rjcd_ad_eqns_t)
               eq(i) = rjcd_ad_eqns_t(cx, pt(i))
            type is(mix_ad_eqns_t)
               eq(i) = mix_ad_eqns_t(cx, pt(i))
            type is(lagp_ad_eqns_t)
               eq(i) = lagp_ad_eqns_t(cx, pt(i))
            class default
               @:ABORT('invalid type')
            end select

         end do

         ! Finish

         return

      end subroutine eqns_factory_

   end function ad_bvp_t_

   !****

   function wave_t_hom_(bp, sm, st, id, chi, n_iter) result(wv)

      class(ad_bvp_t), intent(in)     :: bp
      class(sysmtx_rt), intent(inout) :: sm
      type(state_rt), intent(in)      :: st
      integer, intent(in)             :: id
      real(RD), intent(in)            :: chi
      integer, intent(in)             :: n_iter
      type(wave_t)                    :: wv

      real(RD) :: y(4,bp%n)
      integer  :: j

      ! Calculate the homogeneous solution vector

      call bp%solve_hom(sm, st, y)

      ! Convert to canonical form

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr(j)%transform(st, y(:,j))
      end do

      ! Construct the wave type

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_hom_

   !****

   function wave_t_inhom_(bp, sm, st, z_i, z_o, id, chi, n_iter) result(wv)

      class(ad_bvp_t), intent(in)     :: bp
      class(sysmtx_rt), intent(inout) :: sm
      type(state_rt), intent(in)      :: st
      real(RD), intent(in)            :: z_i(:)
      real(RD), intent(in)            :: z_o(:)
      integer, intent(in)             :: id
      real(RD), intent(in)            :: chi
      integer, intent(in)             :: n_iter
      type(wave_t)                    :: wv

      real(RD) :: y(4,bp%n)
      integer  :: j

      @:CHECK_BOUNDS(SIZE(z_i), bp%n_i)
      @:CHECK_BOUNDS(SIZE(z_o), bp%n_o)

      ! Calculate the inhomogeneous solution vector

      call bp%solve_inhom(sm, st, z_i, z_o, y)

      ! Convert to canonical form

      !$OMP PARALLEL DO
      do j = 1, bp%n
         call bp%tr(j)%transform(st, y(:,j))
      end do

      ! Construct the wave type

      wv = wave_t_y_(bp, st, y, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_inhom_

   !****

   function wave_t_y_(bp, st, y, id, chi, n_iter) result(wv)

      class(ad_bvp_t), intent(in) :: bp
      type(state_rt), intent(in)  :: st
      real(RD), intent(in)        :: y(:,:)
      integer, intent(in)         :: id
      real(RD), intent(in)        :: chi
      integer, intent(in)         :: n_iter
      type(wave_t)                :: wv

      type(state_ct) :: st_c
      complex(RD)    :: y_c(6,bp%n)

      @:CHECK_BOUNDS(SIZE(y, 1), bp%n_e)
      @:CHECK_BOUNDS(SIZE(y, 2), bp%n)

      ! Set up complex eigenfunctions

      st_c = state_ct(CMPLX(st%omega, KIND=RD), st%omega)

      if (bp%os_p%quasiad_eigfuncs) then

         y_c = bp%qe%y_qad(st_c, y)

      else

         y_c(1:4,:) = y
         y_c(5:6,:) = 0._RD

      endif

      ! Construct the wave type

      wv = wave_t(st_c, y_c, bp%cx, bp%gr, bp%md_p, bp%nm_p, bp%os_p, id, chi, n_iter)

      ! Finish

      return

   end function wave_t_y_

end module ad_bvp_m
