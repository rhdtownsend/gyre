! Module  : ad_eqns_m
! Purpose : adiabatic differential equations
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module ad_eqns_m

   ! Uses

   use forum_m, only: RD

   use ad_trans_m
   use context_m
   use eqns_m
   use model_m
   use model_util_m
   use mode_par_m
   use osc_par_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Derived-type definitions

   type, extends(eqns_rt) :: ad_eqns_t
      private
      type(context_t), pointer :: cx => null()
      type(ad_trans_t)         :: tr
      real(RD)                 :: V_g
      real(RD)                 :: As
      real(RD)                 :: As_iso
      real(RD)                 :: U
      real(RD)                 :: c_1
      real(RD)                 :: Omega_rot
      real(RD)                 :: x
   contains
      private
      procedure, public :: eval_A
      procedure, public :: eval_C
   end type ad_eqns_t

   ! Interfaces

   interface ad_eqns_t
      module procedure ad_eqns_t_
   end interface ad_eqns_t

   ! Access specifiers

   public :: ad_eqns_t

   ! Default access

   private

contains

   function ad_eqns_t_(cx, pt, md_p, os_p) result(eq)

      type(context_t), pointer, intent(in) :: cx
      type(point_t), intent(in)            :: pt
      type(mode_par_t), intent(in)         :: md_p
      type(osc_par_t), intent(in)          :: os_p
      type(ad_eqns_t)                      :: eq

      class(model_t), pointer :: ml
      type(point_t)           :: pt_o

      ! Construct the ad_eqns type

      eq%cx => cx

      eq%tr = ad_trans_t(cx, pt, md_p, os_p)

      ! Store structure coefficients

      ml => cx%model()

      call check_model(ml, [I_V_2,I_AS,I_U,I_C_1,I_GAMMA_1,I_OMEGA_ROT])

      @:ASSERT(.NOT. ml%is_vacuum(pt), 'vacuum point')

      eq%V_g = ml%coeff(I_V_2, pt)*pt%x**2/ml%coeff(I_GAMMA_1, pt)
      eq%As = ml%coeff(I_AS, pt)
      eq%As_iso = eq%As*MERGE(MERGE(cx%alpha_pi, cx%alpha_gam, pt%x<cx%x_atm), 1._RD, eq%As > 0._RD)
      eq%U = ml%coeff(I_U, pt)
      eq%c_1 = ml%coeff(I_C_1, pt)

      eq%Omega_rot = cx%Omega_rot(pt)

      ! Other initializations

      eq%x = pt%x

      eq%n_e = 4

      ! Finish

      return

   end function ad_eqns_t_

   !****

   subroutine eval_A(this, st, A)

      class(ad_eqns_t), intent(in) :: this
      class(state_rt), intent(in)  :: st
      real(RD), intent(out)        :: A(:,:)

      real(RD) :: omega_c
      real(RD) :: lambda
      real(RD) :: l_i
      real(RD) :: om2
      real(RD) :: eta
      real(RD) :: invx

      @:CHECK_BOUNDS(SIZE(A, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(A, 2), this%n_e)

      ! Evaluate the Jacobian matrix

      associate ( &
         omega => st%omega, &
         V_g => this%V_g, &
         As => this%As, &
         As_iso => this%As_iso, &
         U => this%U, &
         c_1 => this%c_1, &
         Omega_rot => this%Omega_rot, &
         x => this%x, &
         x_atm => this%cx%x_atm, &
         Omega_rot_i => this%cx%Omega_rot_i, &
         alpha_grv => this%cx%alpha_grv, &
         alpha_omg => this%cx%alpha_omg, &
         alpha_gam => this%cx%alpha_gam, &
         alpha_pi => this%cx%alpha_pi, &
         m => this%cx%m)

         omega_c = omega - m*Omega_rot

         lambda = this%cx%lambda(Omega_rot, st)
         l_i = this%cx%l_e(Omega_rot_i, st)

         om2 = c_1*alpha_omg*omega_c**2
         eta = lambda/om2

         ! Set up the matrix

         invx = 1._RD/x

         A(1,1) = invx*(V_g - 1._RD - l_i)
         A(1,2) = invx*(eta - V_g*alpha_gam)
         A(1,3) = invx*alpha_grv*eta
         A(1,4) = 0._RD

         A(2,1) = invx*(om2 - As_iso)
         A(2,2) = invx*(As - U + 3._RD - l_i)
         A(2,3) = 0._RD
         A(2,4) = invx*alpha_grv*(-1._RD)

         A(3,1) = 0._RD
         A(3,2) = 0._RD
         A(3,3) = invx*alpha_grv*(3._RD - U - l_i)
         A(3,4) = invx*alpha_grv*(1._RD)

         A(4,1) = invx*alpha_grv*(U*As)
         A(4,2) = invx*alpha_grv*(U*V_g)
         A(4,3) = invx*alpha_grv*(lambda)
         A(4,4) = invx*alpha_grv*(-U - l_i + 2._RD)

         ! Apply the variables transformation

         call this%tr%trans_eqns(A, st)

      end associate

      ! Finish

      return

   end subroutine eval_A

   !****

   subroutine eval_C(this, st, C)

      class(ad_eqns_t), intent(in) :: this
      class(state_rt), intent(in)  :: st
      real(RD), intent(out)        :: C(:,:)

      @:CHECK_BOUNDS(SIZE(C, 1), this%n_e)
      @:CHECK_BOUNDS(SIZE(C, 2), this%n_e)

      ! Evaluate the match matrix

      associate ( &
         U => this%U)

         C(1,1) = 1._RD
         C(1,2) = 0._RD
         C(1,3) = 0._RD
         C(1,4) = 0._RD

         C(2,1) = -U
         C(2,2) = U
         C(2,3) = 0._RD
         C(2,4) = 0._RD

         C(3,1) = 0._RD
         C(3,2) = 0._RD
         C(3,3) = 1._RD
         C(3,4) = 0._RD

         C(4,1) = U
         C(4,2) = 0._RD
         C(4,3) = 0._RD
         C(4,4) = 1._RD

         ! Apply the variables transformation

         call this%tr%trans_cond(C, st)

      end associate

      ! Finish

      return

   end subroutine eval_C

end module ad_eqns_m
