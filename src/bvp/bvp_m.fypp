! Module  : bvp_m
! Purpose : parametric boundary value problems
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module bvp_m

   ! Uses

   use forum_m, only: RD

   use bound_m
   use diff_m
   use ext_m
   use num_par_m
   use state_m
   use status_m
   use sysmtx_m
   use sysmtx_factory_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   #:for T, S in zip(DTYPE_BASE_TYPES, DTYPE_SUFFIXES)

      ! Derived-type definitions

      type :: bvp_${S}$
         private
         class(diff_${S}$), allocatable   :: df(:)
         class(bound_${S}$), allocatable  :: bd
         class(sysmtx_${S}$), allocatable :: sm
         integer, public                  :: n
         integer, public                  :: n_e
         integer, public                  :: n_i
         integer, public                  :: n_o
      contains
         private
         procedure, public :: eval_det => eval_det_${S}$_
         procedure, public :: solve_hom => solve_hom_${S}$_
         procedure, public :: solve_inhom => solve_inhom_${S}$_
      end type bvp_${S}$

      ! Interfaces

      interface bvp_${S}$
         module procedure bvp_${S}$_
      end interface bvp_${S}$

      ! Access specifiers

      public :: bvp_${S}$

   #:endfor

   ! Default access

   private

contains

   #:for T, S in zip(DTYPE_BASE_TYPES, DTYPE_SUFFIXES)

      function bvp_${S}$_(bd, df, nm_p) result(bp)

         class(diff_${S}$), intent(in)  :: df(:)
         class(bound_${S}$), intent(in) :: bd
         type(num_par_t), intent(in)    :: nm_p
         type(bvp_${S}$)                :: bp

         integer :: n
         integer :: n_e
         integer :: n_i
         integer :: n_o

         ! Perform basic validations

         n = SIZE(df) + 1
         n_e = bd%n_e

         n_i = bd%n_i
         n_o = bd%n_o

         @:CHECK_BOUNDS(n_i+n_o, n_e)

         @:ASSERT_DEBUG(ALL(df%n_e == n_e), 'dimension mismatch')

         ! Construct the bvp type

         allocate(bp%df(n-1), SOURCE=df)

         allocate(bp%bd, SOURCE=bd)

         allocate(bp%sm, SOURCE=sysmtx_${S}$(n-1, n_e, n_i, n_o, nm_p))

         bp%n = n
         bp%n_e = n_e
         bp%n_i = n_i
         bp%n_o = n_o

         ! Finish

         return

      end function bvp_${S}$_

      !****

      subroutine eval_det_${S}$_(this, st, det)

         class(bvp_${S}$), intent(inout) :: this
         class(state_${S}$), intent(in)  :: st
         type(ext_${S}$), intent(out)    :: det

         ! Evaluate the determinant of the sysmtx

         call this%sm%eval_det(eval_B_, eval_E_, det)

         ! Finish

         return

      contains

         subroutine eval_B_(B_i, B_o)

            ${T}$, intent(out) :: B_i(:,:)
            ${T}$, intent(out) :: B_o(:,:)

            @:CHECK_BOUNDS(SIZE(B_i, 1), this%n_i)
            @:CHECK_BOUNDS(SIZE(B_i, 2), this%n_e)

            @:CHECK_BOUNDS(SIZE(B_o, 1), this%n_o)
            @:CHECK_BOUNDS(SIZE(B_o, 2), this%n_e)

            call this%bd%build_i(st, B_i)
            call this%bd%build_o(st, B_o)

         end subroutine eval_B_

         subroutine eval_E_(k, E_l, E_r, scl)

            integer, intent(in)          :: k
            ${T}$, intent(out)           :: E_l(:,:)
            ${T}$, intent(out)           :: E_r(:,:)
            type(ext_${S}$), intent(out) :: scl

            @:CHECK_BOUNDS(SIZE(E_l, 1), this%n_e)
            @:CHECK_BOUNDS(SIZE(E_l, 2), this%n_e)

            @:CHECK_BOUNDS(SIZE(E_r, 1), this%n_e)
            @:CHECK_BOUNDS(SIZE(E_r, 2), this%n_e)

            call this%df(k)%build(st, E_l, E_r, scl)

         end subroutine eval_E_

      end subroutine eval_det_${S}$_

      !****

      subroutine solve_hom_${S}$_(this, st, y)

         class(bvp_${S}$), intent(inout) :: this
         class(state_${S}$), intent(in)  :: st
         ${T}$, intent(out)              :: y(:,:)

         ${T}$ :: u(this%n_e*this%n)

         @:CHECK_BOUNDS(SIZE(y, 1), this%n_e)
         @:CHECK_BOUNDS(SIZE(y, 2), this%n)

         ! Evaluate the solution vector y of the homogeneous system

         call this%sm%solve_hom(eval_B_, eval_E_, u)

         y = RESHAPE(u, SHAPE(y))

         ! Finish

         return

      contains

         subroutine eval_B_(B_i, B_o)

            ${T}$, intent(out) :: B_i(:,:)
            ${T}$, intent(out) :: B_o(:,:)

            @:CHECK_BOUNDS(SIZE(B_i, 1), this%n_i)
            @:CHECK_BOUNDS(SIZE(B_i, 2), this%n_e)

            @:CHECK_BOUNDS(SIZE(B_o, 1), this%n_o)
            @:CHECK_BOUNDS(SIZE(B_o, 2), this%n_e)

            call this%bd%build_i(st, B_i)
            call this%bd%build_o(st, B_o)

         end subroutine eval_B_

         subroutine eval_E_(k, E_l, E_r, scl)

            integer, intent(in)          :: k
            ${T}$, intent(out)           :: E_l(:,:)
            ${T}$, intent(out)           :: E_r(:,:)
            type(ext_${S}$), intent(out) :: scl

            @:CHECK_BOUNDS(SIZE(E_l, 1), this%n_e)
            @:CHECK_BOUNDS(SIZE(E_l, 2), this%n_e)

            @:CHECK_BOUNDS(SIZE(E_r, 1), this%n_e)
            @:CHECK_BOUNDS(SIZE(E_r, 2), this%n_e)

            call this%df(k)%build(st, E_l, E_r, scl)

         end subroutine eval_E_

      end subroutine solve_hom_${S}$_

      !****

      subroutine solve_inhom_${S}$_(this, st, z_i, z_o, y)

         class(bvp_${S}$), intent(inout) :: this
         class(state_${S}$), intent(in)  :: st
         ${T}$, intent(in)               :: z_i(:)
         ${T}$, intent(in)               :: z_o(:)
         ${T}$, intent(out)              :: y(:,:)

         ${T}$ :: u(this%n_e*this%n)

         @:CHECK_BOUNDS(SIZE(z_i), this%n_i)
         @:CHECK_BOUNDS(SIZE(z_o), this%n_o)

         @:CHECK_BOUNDS(SIZE(y, 1), this%n_e)
         @:CHECK_BOUNDS(SIZE(y, 2), this%n)

         ! Evaluate the solution vector y of the inhomogeneous system

         call this%sm%solve_inhom(eval_B_, eval_E_, [z_i,z_o], u)

         y = RESHAPE(u, SHAPE(y))

         ! Finish

         return

      contains

         subroutine eval_B_(B_i, B_o)

            ${T}$, intent(out) :: B_i(:,:)
            ${T}$, intent(out) :: B_o(:,:)

            @:CHECK_BOUNDS(SIZE(B_i, 1), this%n_i)
            @:CHECK_BOUNDS(SIZE(B_i, 2), this%n_e)

            @:CHECK_BOUNDS(SIZE(B_o, 1), this%n_o)
            @:CHECK_BOUNDS(SIZE(B_o, 2), this%n_e)

            call this%bd%build_i(st, B_i)
            call this%bd%build_o(st, B_o)

         end subroutine eval_B_

         subroutine eval_E_(k, E_l, E_r, scl)

            integer, intent(in)          :: k
            ${T}$, intent(out)           :: E_l(:,:)
            ${T}$, intent(out)           :: E_r(:,:)
            type(ext_${S}$), intent(out) :: scl

            @:CHECK_BOUNDS(SIZE(E_l, 1), this%n_e)
            @:CHECK_BOUNDS(SIZE(E_l, 2), this%n_e)

            @:CHECK_BOUNDS(SIZE(E_r, 1), this%n_e)
            @:CHECK_BOUNDS(SIZE(E_r, 2), this%n_e)

            call this%df(k)%build(st, E_l, E_r, scl)

         end subroutine eval_E_

      end subroutine solve_inhom_${S}$_

   #:endfor

end module bvp_m
