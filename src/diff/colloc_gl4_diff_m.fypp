! Module  : colloc_gl4_diff_m
! Purpose : difference equations (4th-order Gauss-Legendre collocation)
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module colloc_gl4_diff_m

   ! Uses

   use forum_m, only: RD

   use diff_m
   use eqns_m
   use ext_m
   use linalg_m
   use math_m
   use point_m
   use state_m

   use f95_lapack

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Parameter definitions

   #:for S in DTYPE_SUFFIXES

      ! Derived-type definitions

      type, extends(diff_${S}$) :: colloc_gl4_diff_${S}$
         private
         class(eqns_${S}$), allocatable :: eq(:)
         real(RD)                       :: dx
      contains
         private
         procedure, public :: build => build_${S}$_
      end type colloc_gl4_diff_${S}$

      ! Interfaces

      interface colloc_gl4_diff_${S}$
         module procedure colloc_gl4_diff_${S}$_
      end interface colloc_gl4_diff_${S}$

      ! Access specifiers

      public :: colloc_gl4_diff_${S}$

   #:endfor

   ! Default access

   private

contains

   #:for T, S in zip(DTYPE_BASE_TYPES, DTYPE_SUFFIXES)

      function colloc_gl4_diff_${S}$_(eqns_factory, pt_a, pt_b) result(df)

         interface
            subroutine eqns_factory(pt, eq)
               use point_m
               use eqns_m
               implicit none (type, external)
               type(point_t), intent(in)                   :: pt(:)
               class(eqns_${S}$), allocatable, intent(out) :: eq(:)
            end subroutine eqns_factory
         end interface
         type(point_t), intent(in)   :: pt_a
         type(point_t), intent(in)   :: pt_b
         type(colloc_gl4_diff_${S}$) :: df

         real(RD), parameter :: C(2) = [ &
            0.5_RD-sqrt(3._RD)/6._RD, &
            0.5_RD+sqrt(3._RD)/6._RD]

         type(point_t) :: pt(2)

         @:ASSERT_DEBUG(pt_a%s == pt_b%s, 'segment mismatch')

         ! Construct the colloc_gl4_diff type

         pt%s = pt_a%s

         df%dx = pt_b%x - pt_a%x
         pt%x = pt_a%x + C*df%dx

         call eqns_factory(pt, df%eq)

         df%n_e = df%eq(1)%n_e

         ! Finish

         return

      end function colloc_gl4_diff_${S}$_

      !****

      subroutine build_${S}$_(this, st, E_l, E_r, scl)

         class(colloc_gl4_diff_${S}$), intent(in) :: this
         class(state_${S}$), intent(in)           :: st
         ${T}$, intent(out)                       :: E_l(:,:)
         ${T}$, intent(out)                       :: E_r(:,:)
         type(ext_${S}$), intent(out)             :: scl

         real(RD), parameter :: ALPHA = 1._RD/12._RD
         real(RD), parameter :: BETA = SQRT(3._RD)/6._RD
         real(RD), parameter :: GAMMA = 1._RD/4._RD

         ${T}$ :: I(this%n_e,this%n_e)
         ${T}$ :: J(this%n_e,this%n_e)

         ! Build the difference equations

         ! Algorithm note: this is Gauss-Legendre collocation with 2
         ! intermediate points. The implementation follows the
         ! approach in the StORM code

         ! Calculate the jump matrix J. The associate construct
         ! is to clarify re-use of dynamic arrays

         associate( &
            A_1 => E_l, &
            A_2 => E_r, &
            A_21 => J, &
            M => J, &
            K_2 => E_r)

            call this%eq(1)%eval_A(st, A_1)
            A_1 = this%dx*A_1

            call this%eq(2)%eval_A(st, A_2)
            A_2 = this%dx*A_2

            A_21 = MATMUL(A_2, A_1)

            I = identity_matrix(this%n_e)

            K_2 =  A_2 + BETA*A_21
            M = I + ALPHA*A_21

            call LA_GESV(M, K_2)

            J = GAMMA*(MATMUL(A_1, I - BETA*K_2) + K_2)

         end associate

         ! Assemble the difference equations

         E_l = J + I
         E_r = J - I

         scl = ONE_${S}$

         ! Finish

         return

      end subroutine build_${S}$_

   #:endfor

end module colloc_gl4_diff_m
