! Module  : trapz_diff_m
! Purpose : difference equations (quasi-trapezoidal)
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

module trapz_diff_m

   ! Uses

   use forum_m, only: RD

   use diff_m
   use eqns_m
   use ext_m
   use linalg_m
   use point_m
   use state_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   #:for S in DTYPE_SUFFIXES

      ! Derived-type definitions

      type, extends(diff_${S}$) :: trapz_diff_${S}$
         private
         class(eqns_${S}$), allocatable :: eq
         real(RD), allocatable          :: w(:)
         real(RD)                       :: dx
         real(RD)                       :: x_m
         logical                        :: origin
      contains
         private
         procedure, public :: build => build_${S}$_
      end type trapz_diff_${S}$

      ! Interfaces

      interface trapz_diff_${S}$
         module procedure trapz_diff_${S}$_
      end interface trapz_diff_${S}$

      ! Access specifiers

      public :: trapz_diff_${S}$

   #:endfor

   ! Default access

   private

contains

   #:for T, S in zip(DTYPE_BASE_TYPES, DTYPE_SUFFIXES)

      function trapz_diff_${S}$_(eq, pt_a, pt_b, w) result(df)

         class(eqns_${S}$), intent(in) :: eq
         type(point_t), intent(in)     :: pt_a
         type(point_t), intent(in)     :: pt_b
         real(RD), intent(in)          :: w(:)
         type(trapz_diff_${S}$)        :: df

         @:ASSERT_DEBUG(pt_a%s == pt_b%s, 'segment mismatch')

         @:CHECK_BOUNDS(SIZE(w), eq%n_e)

         ! Construct the trapz_diff_t

         df%w = w

         df%dx = pt_b%x - pt_a%x
         df%x_m = 0.5_RD*(pt_a%x + pt_b%x)

         allocate(df%eq, SOURCE=eq)

         call df%eq%stencil([pt_a,pt_b])

         df%origin = pt_a%x == 0._RD

         df%n_e = eq%n_e

         ! Finish

         return

      end function trapz_diff_${S}$_

      !****

      subroutine build_${S}$_(this, st, E_l, E_r, scl)

         class(trapz_diff_${S}$), intent(in) :: this
         class(state_${S}$), intent(in)      :: st
         ${T}$, intent(out)                  :: E_l(:,:)
         ${T}$, intent(out)                  :: E_r(:,:)
         type(ext_${S}$), intent(out)        :: scl

         ${T}$ :: A(this%n_e,this%n_e,2)

         @:CHECK_BOUNDS(SIZE(E_l, 1), this%n_e)
         @:CHECK_BOUNDS(SIZE(E_l, 2), this%n_e)

         @:CHECK_BOUNDS(SIZE(E_r, 1), this%n_e)
         @:CHECK_BOUNDS(SIZE(E_r, 2), this%n_e)

         ! Build the difference equations

         if (this%origin) then

            ! If we're at the origin, fudge things to avoid evaluating A there

            E_l = -identity_matrix(this%n_e)
            E_r =  identity_matrix(this%n_e)

            scl = ONE_${S}$

         else

            ! Evaluate the RHS matrix

            call this%eq%eval_A(1, st, A(:,:,1))
            call this%eq%eval_A(2, st, A(:,:,2))

            ! Build the difference equations

            E_l = -identity_matrix(this%n_e) - this%dx*MATMUL(diagonal_matrix(1._RD-this%w), A(:,:,1))
            E_r =  identity_matrix(this%n_e) - this%dx*MATMUL(diagonal_matrix(      this%w), A(:,:,2))

            scl = ONE_${S}$

         end if

         ! Finish

      end subroutine build_${S}$_

   #:endfor

end module trapz_diff_m
