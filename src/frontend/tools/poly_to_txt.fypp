! Program : poly_to_txt
! Purpose : convert a polytrope to a simple txt format
!
! Copyright 2013-2025 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

program poly_to_txt

   ! Uses

   use forum_m, only: RD, arg_parser_t, OPT_NO_ARG

   use gyre_m
   use poly_file_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Variables

   character(:), allocatable :: in_file_name
   character(:), allocatable :: out_file_name
   logical                   :: drop_outer

   type(arg_parser_t)      :: arg_parser
   type(model_par_t)       :: ml_p
   class(model_t), pointer :: ml
   type(grid_t)            :: gr
   real(RD), allocatable   :: V_2(:)
   real(RD), allocatable   :: As(:)
   real(RD), allocatable   :: U(:)
   real(RD), allocatable   :: c_1(:)
   real(RD), allocatable   :: Gamma_1(:)
   integer                 :: unit
   integer                 :: j

   ! Parse command-line arguments

   drop_outer = .FALSE.

   arg_parser = arg_parser_t('poly_to_txt POLY_FILE TXT_FILE', auto_help=.TRUE.)

   call arg_parser%define_option('drop-outer', OPT_NO_ARG, &
      usage='--drop-outer', description='drop outer point from model')

   call arg_parser%parse(arg_proc, opt_proc)

   if (.NOT. ALLOCATED(in_file_name)) call print_summary()
   if (.NOT. ALLOCATED(out_file_name)) call print_summary()

   ! Read the polytrope data

   ml_p%file = TRIM(in_file_name)

   call read_poly_model(ml_p, ml)

   ! Set up the grid

   gr = ml%grid()

   if (drop_outer) then
      gr = grid_t(gr%pt(:gr%n-1)%x)
   endif

   ! Extract data from the model

   ! Dimensionless structure variables

   allocate(V_2(gr%n))
   allocate(As(gr%n))
   allocate(U(gr%n))
   allocate(c_1(gr%n))
   allocate(Gamma_1(gr%n))

   do j = 1, gr%n
      associate (pt => gr%pt(j))
         V_2(j) = ml%coeff(I_V_2, pt)
         As(j) = ml%coeff(I_AS, pt)
         U(j) = ml%coeff(I_U, pt)
         c_1(j) = ml%coeff(I_C_1, pt)
         Gamma_1(j) = ml%coeff(I_GAMMA_1, pt)
      end associate
   end do

   ! Write out the txt file

   open(NEWUNIT=unit, FILE=out_file_name, STATUS='REPLACE')

   write(unit, 100) 'x V_2 As U c_1 Gamma_1'
100 format(A)

   do j = 1, gr%n
      write(unit, 110) gr%pt(j)%x, V_2(j), As(j), U(j), c_1(j), Gamma_1(j)
110   format(6(1X,E26.18E3))
   end do

   close(unit)

   ! Finish

contains

   subroutine arg_proc(a, value)

      integer, intent(in)      :: a
      character(*), intent(in) :: value

      select case(a)
      case(1)
         in_file_name = value
      case(2)
         out_file_name = value
      case default
         call print_summary()
      end select

   end subroutine arg_proc

   !****

   subroutine opt_proc(name, value)

      character(*), intent(in) :: name
      character(*), intent(in) :: value

      integer :: stat

      stat = STAT_OK

      select case(name)
      case('drop-outer')
         drop_outer = .TRUE.
      case('help')
         call print_summary()
      case default
         @:ABORT('invalid option name')
      end select

      if (stat /= STAT_OK) then
         @:STOP('invalid --'//name)
      end if

   end subroutine opt_proc

   !****

   subroutine print_summary()

      call arg_parser%print_summary()
      @:STOP()

   end subroutine print_summary

end program poly_to_txt
